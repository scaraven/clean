module

public import Mathlib.Algebra.Field.Basic
public import Clean.Circuit.SimpGadget

@[expose] public section

variable {F : Type}

structure Variable (F : Type) where
  index : ℕ

instance : Repr (Variable F) where
  reprPrec v _ := "var ⟨" ++ repr v.index ++ "⟩"

inductive Expression (F : Type) where
  | var : Variable F -> Expression F
  | const : F -> Expression F
  | add : Expression F -> Expression F -> Expression F
  | mul : Expression F -> Expression F -> Expression F

export Expression (var)

/-- Arbitrary data a prover can witness and refer to in a circuit spec -/
def ProverData (F : Type) :=
  String → (n : ℕ) → Array (Vector F n)

/-- Runtime-only hashmap of prover hints. Same shape as `ProverData`, but
distinct to emphasize that hints are *not* committed: they feed only the
witness-generation step and never appear in the proof. -/
def ProverHint (F : Type) :=
  String → (n : ℕ) → Array (Vector F n)

/-- Placeholder `ProverHint` that returns an empty array for every key. -/
def ProverHint.empty (F : Type) : ProverHint F := fun _ _ => #[]

instance : Inhabited (ProverHint F) where
  default := ProverHint.empty F

/--
  `Environment` represents the data that is provided at runtime to concretely
  specify the witness assignment of a circuit (`get`) and any additional witness data
  external to the current circuit (`data`).

  In the abstract plaintext-witness version of the protocol, the full
  `Environment` is passed from the prover to the verifier.
  All constraints can be checked against the `Environment`.
  Soundness theorems have the form `∀ env : Environment F, ...`.
 -/
structure Environment (F : Type) where
  /-- Assignment of a circuit's variables to field elements -/
  get : ℕ → F
  /-- Additional witness data not part of the current circuit's witness, such as the content
   of lookup tables, made available for potential re-witnessing and for statements concerning
   the verifier, such as a circuit's spec. -/
  data : ProverData F

/--
  `ProverEnvironment` is `Environment` plus the prover's runtime `ProverHint`.
  In some circuits, the additional `hint` is necessary to give an honest prover
  sufficient information to generate a witness.
  Completeness theorems are formulated against the `ProverEnvironment`.
-/
structure ProverEnvironment (F : Type) extends Environment F where
  /-- Runtime-only hashmap of prover hints, never committed into the proof. -/
  hint : ProverHint F

/-- Project a `ProverEnvironment` to its `Environment`. -/
instance : Coe (ProverEnvironment F) (Environment F) := ⟨ProverEnvironment.toEnvironment⟩
instance : CoeOut (ProverEnvironment F) (Environment F) := ⟨ProverEnvironment.toEnvironment⟩

instance {α} : Coe (Environment F → α) (ProverEnvironment F → α) := ⟨fun f env => f env⟩
instance {α} : CoeOut (Environment F → α) (ProverEnvironment F → α) := ⟨fun f env => f env⟩

@[circuit_norm] abbrev Environment.fromArray [Field F] (row : Array F) (data : ProverData F) : Environment F where
  get j := row[j]?.getD 0
  data

namespace Expression
variable [Field F]

/--
Evaluate expression given an external `environment` that determines the assignment
of all variables.

This is needed when we want to make statements about a circuit in the adversarial
situation where the prover can assign anything to variables.
-/
@[circuit_norm]
def eval (env : Environment F) : Expression F → F
  | var v => env.get v.index
  | const c => c
  | add x y => eval env x + eval env y
  | mul x y => eval env x * eval env y

def toString [Repr F] : Expression F → String
  | var v => reprStr v
  | const c => reprStr c
  | add x y => "(" ++ toString x ++ " + " ++ toString y ++ ")"
  | mul x y => "(" ++ toString x ++ " * " ++ toString y ++ ")"

instance [Repr F] : Repr (Expression F) where
  reprPrec e _ := toString e

-- combine expressions elegantly
instance : Zero (Expression F) where zero := const 0
instance : One (Expression F) where one := const 1
instance : Add (Expression F) where add := add
instance : Neg (Expression F) where neg e := mul (const (-1)) e
instance : Sub (Expression F) where sub e₁ e₂ := add e₁ (-e₂)
instance : Mul (Expression F) where mul := mul

/- The `Neg`/`Sub` instances above (and their witness-IR `FExpr` counterparts) desugar
`-e` to `(-1) * e` and `e₁ - e₂` to `e₁ + (-1) * e₂`, so *evaluated* goals come out
spelled `x + -1 * y`. Normalize back to `-x` / `x - y` in `circuit_norm`, so user-facing
goals read like the circuit source and `-`-spelled lemmas apply directly (previously
every proof needed `sub_eq_add_neg` bridges). -/
attribute [circuit_norm] neg_one_mul

@[circuit_norm]
theorem add_neg_eq_sub {α : Type*} [SubtractionMonoid α] (a b : α) :
    a + -b = a - b := (sub_eq_add_neg a b).symm

/- Boolean conjunctions from witness-IR `&&&` conditions surface as `(a && b) = true`;
normalize to the propositional conjunction (companion to `decide_eq_true_eq`, tagged in
`Clean.Circuit.WitnessIR`). -/
attribute [circuit_norm] Bool.and_eq_true

instance : Coe F (Expression F) where coe f := const f
instance {n : ℕ} [OfNat F n] : OfNat (Expression F) n where
  ofNat := const (OfNat.ofNat n)

instance : HMul F (Expression F) (Expression F) where hMul f e := mul f e
instance : HMul (Expression F) F (Expression F) where hMul f e := mul f e

instance : HDiv (Expression F) F (Expression F) where hDiv e f := mul (f⁻¹ : F) e
instance : HDiv (Expression F) ℕ (Expression F) where hDiv e f := mul (f⁻¹ : F) e

-- TODO probably should just make Variable F := ℕ
instance {n : ℕ} : OfNat (Variable F) n where
  ofNat := { index := n }
end Expression

instance [Field F] : CoeFun (Environment F) (fun _ => (Expression F) → F) where
  coe env x := x.eval env

instance [Field F] : CoeFun (ProverEnvironment F) (fun _ => (Expression F) → F) where
  coe env x := x.eval env

instance [Field F] : Inhabited F where
  default := 0

instance [Field F] : Inhabited (Expression F) where
  default := .const 0

/-! ## Lemmas about Expression evaluation -/

section EvalLemmas
variable [Field F]

/-- Expression.eval distributes over multiplication -/
@[circuit_norm]
lemma eval_mul (env : Environment F) (a b : Expression F) :
    Expression.eval env (Expression.mul a b) = (Expression.eval env a) * (Expression.eval env b) := by
  simp only [Expression.eval]

/-- Expression.eval distributes over addition -/
@[circuit_norm]
lemma eval_add (env : Environment F) (a b : Expression F) :
    Expression.eval env (Expression.add a b) = (Expression.eval env a) + (Expression.eval env b) := by
  simp only [Expression.eval]

/-- Expression.eval distributes over negation. Keyed on the `-a` surface syntax: the
`Expression.eval` matcher does not unfold the composite `Neg`/`Sub` instances, so the
`Expression.mul`-keyed lemmas do not reach these spellings. -/
@[circuit_norm]
lemma eval_neg (env : Environment F) (a : Expression F) :
    Expression.eval env (-a) = -Expression.eval env a := by
  show Expression.eval env (Expression.mul (Expression.const (-1)) a) = _
  simp only [Expression.eval, neg_one_mul]

/-- Expression.eval distributes over subtraction (see `eval_neg`). -/
@[circuit_norm]
lemma eval_sub (env : Environment F) (a b : Expression F) :
    Expression.eval env (a - b) = Expression.eval env a - Expression.eval env b := by
  show Expression.eval env (Expression.add a (-b)) = _
  rw [eval_add, eval_neg, ← sub_eq_add_neg]

/-- Expression.eval distributes over Fin.foldl with addition -/
lemma eval_foldl (env : Environment F) (n : ℕ)
    (f : Expression F → Fin n → Expression F) (init : Expression F)
    (hf : ∀ (e : Expression F) (i : Fin n),
      Expression.eval env (f e i) = Expression.eval env (f (Expression.const (Expression.eval env e)) i)) :
    Expression.eval env (Fin.foldl n f init) =
    Fin.foldl n (fun (acc : F) (i : Fin n) => Expression.eval env (f (Expression.const acc) i)) (Expression.eval env init) := by
  induction n with
  | zero => simp [Fin.foldl_zero]
  | succ n' ih =>
    rw [Fin.foldl_succ_last, Fin.foldl_succ_last]
    -- Apply the inductive hypothesis with the appropriate function and assumption
    have hf' : ∀ (e : Expression F) (i : Fin n'),
      Expression.eval env (f e i.castSucc) = Expression.eval env (f (Expression.const (Expression.eval env e)) i.castSucc) := by
      intros e i
      exact hf e i.castSucc

    have h1 : Expression.eval env (Fin.foldl n' (fun x1 x2 => f x1 x2.castSucc) init) =
              Fin.foldl n' (fun acc i => Expression.eval env (f (Expression.const acc) i.castSucc)) (Expression.eval env init) :=
      ih (fun x i => f x i.castSucc) hf'

    -- Now apply the assumption to relate the two sides
    rw [hf (Fin.foldl n' (fun x1 x2 => f x1 x2.castSucc) init) (Fin.last n')]
    -- Rewrite using h1
    rw [h1]

end EvalLemmas
