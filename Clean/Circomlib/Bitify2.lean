module

public import Clean.Circuit
public import Clean.Utils.Bits
public import Clean.Utils.Fin
public import Clean.Circomlib.Bitify
public import Clean.Circomlib.AliasCheck
public import Clean.Circomlib.Comparators

@[expose] public section

/-
Original source code:
https://github.com/iden3/circomlib/blob/35e54ea21da3e8762557234298dbb553c175ea8d/circuits/bitify.circom

This file contains the templates from bitify.circom that couldn't be included in Bitify.lean
due to cyclic import dependencies with AliasCheck.
-/

namespace Circomlib
open Utils.Bits
variable {p : ℕ} [Fact p.Prime] [Fact (p < 2^254)] [Fact (p > 2^253)]

namespace Num2Bits_strict
/-
template Num2Bits_strict() {
    signal input in;
    signal output out[254];

    component aliasCheck = AliasCheck();
    component n2b = Num2Bits(254);
    in ==> n2b.in;

    for (var i=0; i<254; i++) {
        n2b.out[i] ==> out[i];
        n2b.out[i] ==> aliasCheck.in[i];
    }
}
-/
def main (input : Expression (F p)) := do
  -- Convert input to 254 bits
  let bits ← Num2Bits.main 254 input

  -- Check that the bits represent a value less than p
  AliasCheck.circuit bits

  return bits

set_option linter.constructorNameAsVariable false

def circuit : FormalCircuit (F p) field (fields 254) where
  main

  Spec input bits :=
    bits = fieldToBits 254 input

  soundness := by
    intro i0 env input_var input h_input assumptions h_holds
    simp only [circuit_norm, main] at h_holds ⊢
    dsimp only [Num2Bits.main, AliasCheck.circuit] at h_holds ⊢
    simp_all only [circuit_norm, Vector.map_mapRange]
    simp only [Num2Bits.lc_eq, Fin.forall_iff,
      mul_eq_zero, sub_eq_zero] at h_holds
    obtain ⟨ ⟨h_bits, h_eq⟩, h_alias ⟩ := h_holds
    specialize h_alias h_bits
    rw [← h_eq, fieldToBits, fieldFromBits,
      ZMod.val_natCast, Vector.map_mapRange]
    rw [Nat.mod_eq_of_lt h_alias, toBits_fromBits, Vector.ext_iff]
    simp only [circuit_norm]
    intro i hi
    simp only [circuit_norm]
    specialize h_bits i hi
    rcases h_bits with h_bits | h_bits
      <;> simp [h_bits, ZMod.val_one]

  completeness := by
    intro i0 env input_var h_env input h_input assumptions
    simp only [circuit_norm, main, Num2Bits.main] at h_env h_input ⊢
    dsimp only [circuit_norm, AliasCheck.circuit] at h_env ⊢
    simp only [h_input, circuit_norm] at h_env ⊢
    simp only [Num2Bits.lc_eq, Fin.forall_iff,
      mul_eq_zero, sub_eq_zero] at h_env ⊢
    rw [Vector.map_mapRange]
    simp only [Expression.eval]
    have h_bits i (hi : i < 254) : env.get (i0 + i) = 0 ∨ env.get (i0 + i) = 1 := by
      rw [h_env i hi]
      rcases Nat.mod_two_eq_zero_or_one (ZMod.val input >>> i) with h | h <;> simp [h]
    set bits := Vector.mapRange 254 fun i => env.get (i0 + i)
    have h_eq : bits = fieldToBits 254 input := by
      ext i hi; simp only [bits, Vector.getElem_mapRange, h_env i hi, getElem_fieldToBits]
    have input_lt : input.val < 2^254 := by
      linarith [‹Fact (p < 2^254)›.elim, ZMod.val_lt input]
    use h_bits
    simp_rw [h_eq, fieldFromBits_fieldToBits input_lt,
      fieldToBits, Vector.map_map, val_natCast_toBits,
      fromBits_toBits input_lt, ZMod.val_lt]
    use trivial, h_bits
end Num2Bits_strict

namespace Bits2Num_strict
/-
template Bits2Num_strict() {
    signal input in[254];
    signal output out;

    component aliasCheck = AliasCheck();
    component b2n = Bits2Num(254);

    for (var i=0; i<254; i++) {
        in[i] ==> b2n.in[i];
        in[i] ==> aliasCheck.in[i];
    }

    b2n.out ==> out;
}
-/
def main (input : Vector (Expression (F p)) 254) := do
  -- Check that the bits represent a value less than p
  AliasCheck.circuit input

  -- Convert bits to number
  Bits2Num.circuit 254 input

set_option linter.constructorNameAsVariable false

def circuit : GeneralFormalCircuit (F p) (fields 254) field where
  main

  ProverAssumptions (input : fields 254 (F p)) _ _ :=
    (∀ i (_ : i < 254), input[i] = 0 ∨ input[i] = 1) ∧ fromBits (input.map ZMod.val) < p
  Assumptions (input : fields 254 (F p)) _ :=
    (∀ i (_ : i < 254), input[i] = 0 ∨ input[i] = 1)
  Spec (input : fields 254 (F p)) output _ :=
    output.val = fromBits (input.map ZMod.val)

  soundness := by
    circuit_proof_start [AliasCheck.circuit, Bits2Num.circuit]
    have h_lt := h_holds.1 h_assumptions
    have h_value := (h_holds.2 h_assumptions).1
    rw [h_value, fieldFromBits, ZMod.val_natCast_of_lt h_lt]

  completeness := by
    circuit_proof_start [AliasCheck.circuit, Bits2Num.circuit]
    exact ⟨h_assumptions, h_assumptions.1⟩
end Bits2Num_strict

namespace Num2BitsNeg
/-
template Num2BitsNeg(n) {
    signal input in;
    signal output out[n];
    var lc1=0;

    component isZero;

    isZero = IsZero();

    var neg = n == 0 ? 0 : 2**n - in;

    for (var i = 0; i < n; i++) {
        out[i] <-- (neg >> i) & 1;
        out[i] * (out[i] -1 ) === 0;
        lc1 += out[i] * 2**i;
    }

    in ==> isZero.in;

    lc1 + isZero.out * 2**n === 2**n - in;
}
-/
def main (n : ℕ) (input : Expression (F p)) := do
  -- Witness the bits of 2^n - input (when n > 0)
  let diff : Expression (F p) := (2^n : F p) - input
  let out ← witnessVector n (diff.bits n)

  -- Constrain each bit to be 0 or 1 and compute linear combination
  let lc1 ← Circuit.foldlRange n 0 fun lc1 i => do
    assertBool out[i]
    return lc1 + out[i] * (2^i.val : F p)

  -- Check if input is zero
  let isZero_out ← IsZero.circuit input

  -- Main constraint: lc1 + isZero.out * 2^n === 2^n - in
  lc1 + isZero_out * (2^n : F p) === (2^n : F p) - input

  return out

def circuit (n : ℕ) (hn : 2^n < p) : GeneralFormalCircuit (F p) field (fields n) where
  main := main n

  ProverAssumptions input _ _ := input.val < 2^n

  Spec input output _ :=
    output = fieldToBits n (if n = 0 then 0 else 2^n - input.val : F p)

  soundness := by
    intro i0 env input_var (input : F p) h_input _ h_holds
    simp only [circuit_norm] at h_input
    simp only [circuit_norm, main, IsZero.circuit, h_input] at h_holds ⊢
    obtain ⟨ h_bits, h_iszero, h_eq ⟩ := h_holds

    by_cases h_n : n = 0
    · rw [h_n] at h_eq h_iszero ⊢
      simp_all only [Nat.reducePow, gt_iff_lt, pow_zero, add_zero, lt_self_iff_false,
        ↓reduceDIte, Fin.foldl_zero, mul_one, ↓reduceIte]
      unfold Vector.mapRange
      simp only [Vector.map_mk, List.map_toArray, List.map_nil, Vector.mk_eq, Array.empty_eq,
        Vector.toArray_eq_empty_iff]
    · set bits := Vector.map (Expression.eval env) (Vector.mapRange n fun i => var { index := i0 + i })
      have h_bits' : ∀ (i : ℕ) (hi : i < n), bits[i] = 0 ∨ bits[i] = 1 := by
        intro i hi
        simp only [bits, Vector.getElem_map, Vector.getElem_mapRange]
        apply h_bits ⟨i, hi⟩

      set bits_vars := Vector.mapRange n fun i => var (F := F p) { index := i0 + i }
      have h_fold : (Fin.foldl n (fun acc i ↦ acc + var { index := i0 + ↑i } * Expression.const (2 ^ (Fin.val i))) 0)
          = fieldFromBitsExpr bits_vars := by
        simp [fieldFromBitsExpr, bits_vars, Vector.getElem_mapRange]

      by_cases h_input_zero : input = 0
      · subst h_input_zero
        simp only [dite_eq_ite, sub_zero] at h_eq ⊢
        rw [← h_eq]
        have h_f := fieldToBits_fieldFromBits hn bits h_bits'
        simp_all only [Nat.reducePow, gt_iff_lt, dite_eq_ite, ↓reduceIte, one_mul,
          add_eq_right, zero_add]
        ext i hi
        simp only [fieldToBits, toBits, Vector.getElem_mapRange, Vector.getElem_map]
        rw [← Nat.cast_two, ← Nat.cast_pow, ZMod.val_natCast_of_lt hn, Nat.testBit_two_pow]
        have : n ≠ i := ne_of_gt hi
        simp only [this]
        rw [← fieldToBits_fieldFromBits hn bits h_bits']
        have h_val_zero : fieldFromBits bits = 0 := by
          simp only [fieldFromBits_eval] at h_eq
          have h_bits_eq : Vector.map (Expression.eval env) bits_vars = bits := by
            simp only [bits, bits_vars]
          rw [h_bits_eq] at h_eq
          exact h_eq
        rw [h_val_zero]
        simp [fieldToBits, toBits, Vector.getElem_mapRange]
      · simp_all only [↓reduceIte, dite_eq_ite, add_zero, zero_mul]
        rw [← h_eq]
        simp only [fieldFromBits_eval]
        rw [fieldToBits_fieldFromBits hn]
        exact h_bits'

  completeness := by
    simp only [circuit_norm, main]
    intro i0 env input_var h_env input h_input assumption
    simp only [circuit_norm, IsZero.circuit] at h_env h_input ⊢
    simp only [h_input, circuit_norm] at h_env ⊢
    by_cases h_n : n = 0
    · rw [h_n] at h_env ⊢
      simp_all only [Nat.reducePow, gt_iff_lt, pow_zero, IsEmpty.forall_iff,
        add_zero, lt_self_iff_false, ↓reduceDIte, true_and, Fin.foldl_zero, mul_one]
      by_cases h_input_zero : input = 0
      · rw [h_input_zero]
        simp only [Expression.eval, ↓reduceIte, zero_add, sub_zero]
      · simp_all only [↓reduceIte, add_zero]
        simp only [Expression.eval]
        rw [← h_env]
        rw [← h_input]
        simp_all
    · obtain ⟨h_bits, h_eq⟩ := h_env
      constructor
      · intro i
        rw [h_bits]
        simp only [IsBool]
        rcases Nat.mod_two_eq_zero_or_one (ZMod.val ((2 ^ n : F p) - input) >>> i.val) with h | h <;>
          rw [h] <;> simp
      · rw [h_eq]
        simp_all only [Nat.reducePow, gt_iff_lt, dite_eq_ite, ite_mul, one_mul, zero_mul]
        let bits_vars := Vector.mapRange n fun i => var (F := F p) { index := i0 + i }

        have h_expr_fold : (Fin.foldl n (fun acc i ↦ acc + var { index := i0 + ↑i } * Expression.const (2 ^ (Fin.val i))) 0)
            = fieldFromBitsExpr bits_vars := by
          simp only [fieldFromBitsExpr, bits_vars, Vector.getElem_mapRange]
        rw [h_expr_fold]

        have : ∀ (i: Fin n), Expression.eval env bits_vars[i]! = env.get (i0 + i) := by
          unfold bits_vars
          simp_all only [Fin.is_lt, getElem!_pos, Fin.getElem_fin]
          intro i
          rw [← h_bits]
          simp only [Vector.getElem_mapRange, Expression.eval]

        simp_all only [Fin.is_lt, getElem!_pos, Fin.getElem_fin]
        simp only [fieldFromBits_eval]

        by_cases h_iz: input = 0
        · have hval : (ZMod.val (2 ^ n: ZMod p)) = 2 ^ n := by
            rw [← ZMod.val_natCast_of_lt hn]
            simp only [Nat.cast_pow, Nat.cast_ofNat]

          simp_all only [sub_zero, ↓reduceIte]
          have hbit0 : ∀ (i : Fin n), Expression.eval env.toEnvironment bits_vars[i.val] = 0 := by
            intro i
            rw [this i]
            have hz : (2 ^ n >>> ↑i) % 2 = 0 := by
              rw [Nat.shiftRight_eq_div_pow, Nat.pow_div (le_of_lt i.isLt) (by norm_num)]
              rw [← Nat.sub_add_cancel (Nat.one_le_iff_ne_zero.mpr (Nat.sub_ne_zero_of_lt i.isLt)),
                pow_succ, Nat.mul_mod_left]
            simp [hz]
          have hzero : fieldFromBits (Vector.map (fun x ↦ Expression.eval env.toEnvironment x) bits_vars) = 0 := by
            rw [fieldFromBits_as_sum]
            simp only [Vector.getElem_map]
            conv_lhs =>
              congr
              ext acc k
              simp only [hbit0 k, zero_mul, add_zero]
            apply Fin.fin_foldl_const
          rw [hzero, zero_add]
        · have h_field_eq : ((2 ^ n - input.val : ℕ) : F p) = (2 ^ n : F p) - (ZMod.cast input : F p) := by
            rw [Nat.cast_sub (Nat.le_of_lt assumption)]
            simp only [Nat.cast_pow, Nat.cast_ofNat]
            congr
            simp only [ZMod.natCast_val]

          have h_1: fieldFromBits ((fieldToBits n ((2 ^ n) - input.val).cast)) = ((fieldFromBits (Vector.map (fun x ↦ Expression.eval env x) bits_vars))) := by
            apply fieldFromBits_eq
            intro i
            simp only [Fin.getElem_fin, Vector.getElem_map]
            rw [this, h_field_eq, ZMod.cast_id, getElem_fieldToBits, sub_eq_add_neg]

          simp_all only [↓reduceIte, add_zero]
          rw [← h_1]
          rw [ZMod.cast_id]
          rw [fieldFromBits_fieldToBits]

          have hnowrap : 2 ^ n - ZMod.val input < p :=
            lt_of_le_of_lt (Nat.sub_le _ _) hn

          have h_val_eq : HSub.hSub (2^n : F p) ↑input = ↑(2 ^ n - ZMod.val input) := by
            rw [Nat.cast_sub (Nat.le_of_lt assumption)]
            simp only [Nat.cast_pow, Nat.cast_ofNat, ZMod.natCast_val, sub_right_inj]
            rw [ZMod.cast_id]

          rw [h_val_eq]
          simp only [ZMod.val_natCast_of_lt hnowrap]
          simp only [tsub_lt_self_iff, Nat.ofNat_pos, pow_pos, ZMod.val_pos, ne_eq, true_and]
          exact h_iz
end Num2BitsNeg

end Circomlib
