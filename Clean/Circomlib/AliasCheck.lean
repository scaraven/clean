module

public import Clean.Circuit
public import Clean.Utils.Bits
public import Clean.Circomlib.CompConstant

@[expose] public section

/-
Original source code:
https://github.com/iden3/circomlib/blob/35e54ea21da3e8762557234298dbb553c175ea8d/circuits/aliascheck.circom
-/

namespace Circomlib
open Utils.Bits
variable {p : ℕ} [Fact p.Prime] [Fact (p < 2^254)] [Fact (p > 2^253)]
instance hp135 : Fact (p > 2^135) := .mk (by linarith [‹Fact (p > 2^253)›.elim])
instance hppre254 : Fact (p - 1 < 2^254) := .mk (by
  calc
    p - 1 ≤ p := by grind
    _ < _ := by linarith [‹Fact (p < 2^254)›.elim])

namespace AliasCheck
/-
template AliasCheck() {

    signal input in[254];

    component  compConstant = CompConstant(-1);

    for (var i=0; i<254; i++) in[i] ==> compConstant.in[i];

    compConstant.out === 0;
}
-/
def main (input : Vector (Expression (F p)) 254) := do
  -- CompConstant(-1) means we're comparing against p-1 (since -1 ≡ p-1 mod p)
  let comp_out ← CompConstant.circuit (p - 1) hppre254.elim input
  comp_out === 0

def circuit : FormalAssertion (F p) (fields 254) where
  main

  Assumptions input := ∀ i (_ : i < 254), input[i] = 0 ∨ input[i] = 1

  Spec bits := fromBits (bits.map ZMod.val) < p

  soundness := by
    circuit_proof_start [CompConstant.circuit]
    obtain ⟨ h_comp, h_eq ⟩ := h_holds
    simp_all only [implies_true, right_eq_ite_iff, zero_ne_one, imp_false, forall_const]
    have : p > 2^135 := hp135.elim
    omega

  completeness := by
    circuit_proof_start [CompConstant.circuit]
    simp_all
    have ppre_small : p - 1 < 2^254 := hppre254.elim
    omega
end AliasCheck

end Circomlib
