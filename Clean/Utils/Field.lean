module

public import Mathlib.Algebra.Field.ZMod
public import Mathlib.Algebra.Order.Star.Basic
public import Mathlib.Analysis.Normed.Ring.Lemmas
public import Clean.Circuit.SimpGadget

@[expose] public section

-- main field definition
abbrev F p := ZMod p
instance (p : ℕ) [Fact p.Prime]: Field (F p) := ZMod.instField p
instance (p : ℕ) [Fact p.Prime] : Fintype (F p) := ZMod.fintype p
instance (p : ℕ) [Fact p.Prime] : Inhabited (F p) := ⟨0⟩
instance (p : ℕ) : CommRing (F p) := ZMod.commRing p

instance {p : ℕ} : DecidableEq (F p) := ZMod.decidableEq p

@[circuit_norm]
lemma zero_ne_neg_one {F : Type} [Field F] : (0 : F) ≠ -1 := by
  intro h
  apply zero_ne_one (α:=F)
  rw [← neg_zero, h, neg_neg]

namespace FieldUtils
variable {p : ℕ} [p_prime: Fact p.Prime]

instance : NeZero p := ⟨p_prime.elim.ne_zero⟩

theorem p_ne_zero : p ≠ 0 := p_prime.elim.ne_zero

theorem ext {x y : F p} (h : x.val = y.val) : x = y := by
  cases p; cases p_ne_zero rfl
  exact Fin.ext h

theorem ext_iff {x y : F p} : x = y ↔ x.val = y.val := by
  constructor; simp_all; apply ext

theorem val_lt_p {p : ℕ} (x : ℕ) : (x < p) → (x : F p).val = x := by
  intro x_lt_p
  have p_ne_zero : p ≠ 0 := Nat.ne_zero_of_lt x_lt_p
  have x_mod_is_x : x % p = x := (Nat.mod_eq_iff_lt p_ne_zero).mpr x_lt_p
  rw [ZMod.val_natCast, x_mod_is_x]

lemma val_eq_256 [p_large_enough: Fact (p > 512)] : (256 : F p).val = 256 := val_lt_p 256 (by linarith [p_large_enough.elim])

/--
tactic script to fully rewrite a ZMod expression to its Nat version, given that
the expression is smaller than the modulus.

```
example (x y : F p) (hx : x.val < 256) (hy : y.val < 2) :
  (x + y * 256).val = x.val + y.val * 256 := by field_to_nat
```

expected context:
- the equation to prove as the goal
- size assumptions on variables and a sufficient `p > ...` instance

if no sufficient inequalities are in the context, then the tactic will leave an equation of the form `expr : Nat < p` unsolved.

note: this version is optimized specifically for byte arithmetic:
- specifically handles field constant 256
- expects `[Fact (p > 512)]` in the context
-/
syntax "field_to_nat" : tactic
macro_rules
  | `(tactic|field_to_nat) =>
    `(tactic|(
      intros
      repeat rw [ZMod.val_add] -- (a + b).val = (a.val + b.val) % p
      repeat rw [ZMod.val_mul] -- (a * b).val = (a.val * b.val) % p
      repeat rw [val_eq_256]
      try simp only [Nat.add_mod_mod, Nat.mod_add_mod, Nat.mul_mod_mod, Nat.mod_mul_mod]
      rw [Nat.mod_eq_of_lt _]
      repeat linarith [‹Fact (_ > 512)›.elim]))

example [Fact (p > 512)] (x y : F p) (hx : x.val < 256) (hy : y.val < 2) :
    (x + y * 256).val = x.val + y.val * 256 := by field_to_nat

def natToField (n : ℕ) (lt : n < p) : F p :=
  match p with
  | 0 => False.elim (Nat.not_lt_zero n lt)
  | _ + 1 => ⟨ n, lt ⟩

theorem natToField_zero : natToField 0 (p_prime.elim.pos) = 0 := by
  dsimp [natToField]
  cases p
  · exact False.elim (Nat.not_lt_zero 0 p_prime.elim.pos)
  · rfl

theorem natToField_eq {n : ℕ} {lt : n < p} (x : F p) (hx : x = natToField n lt) : x.val = n := by
  cases p
  · exact False.elim (Nat.not_lt_zero n lt)
  · rw [hx]; rfl

theorem natToField_of_val_eq_iff {x : F p} {lt : x.val < p} : natToField (x.val) lt = x := by
  cases p
  · exact False.elim (Nat.not_lt_zero x.val lt)
  · dsimp only [natToField]; rfl

theorem natToField_eq_natCast {n : ℕ} (lt : n < p) : ↑n = FieldUtils.natToField n lt := by
  apply ext
  rw [val_lt_p n lt, natToField_eq _ rfl]

theorem natToField_val {n : ℕ} (lt : n < p) : (natToField n lt).val = n := by
  cases p
  · exact False.elim (Nat.not_lt_zero n lt)
  · rfl

theorem less_than_p (x : F p) : x.val < p := by
  rcases p with _ | n; cases p_ne_zero rfl
  exact x.is_lt

@[circuit_norm, grind =]
lemma natCast_val (a : F p) : (a.val : F p) = a := ZMod.natCast_zmod_val _

@[circuit_norm, grind .]
lemma fin_val_natCast_val_lt {p : ℕ} {n : ℕ} (x : Fin n) : (x.val : F p).val < n := by
  grw [ZMod.val_natCast, Nat.mod_le]
  exact x.isLt

@[grind =]
lemma fin_val_natCast_val_eq_of_lt {p : ℕ} {n : ℕ} (x : Fin n) (hn : n < p) : (x.val : F p).val = x.val := by
  rw [ZMod.val_natCast_of_lt (a := x.val)]
  linarith [hn, x.isLt]

def mod (x : F p) (c : ℕ+) (_lt : c < p) : F p := (x.val % c : ℕ)

def floorDiv (x : F p) (c : ℕ+) : F p := (x.val / c : ℕ)

lemma mod_val {x : F p} {c : ℕ+} {lt : c < p} : (mod x c lt).val = x.val % c := by
  rw [mod, ZMod.val_natCast_of_lt]
  grw [Nat.mod_lt x.val c.pos]
  exact lt.le

lemma floorDiv_val {x : F p} {c : ℕ+} : (floorDiv x c).val = x.val / c := by
  rw [floorDiv, ZMod.val_natCast_of_lt]
  grw [Nat.div_le_self]
  apply ZMod.val_lt

theorem mod_lt {x : F p} {c : ℕ+} {lt : c < p} : (mod x c lt).val < c := by
  rw [mod_val]
  exact Nat.mod_lt x.val (by norm_num)

theorem floorDiv_lt {x : F p} {c : ℕ+} {d : ℕ} (h : x.val < c * d) : (floorDiv x c).val < d := by
  rw [floorDiv_val]
  exact Nat.div_lt_of_lt_mul h

lemma val_mul_floorDiv_self {x : F p} {c : ℕ+} (lt : c < p) : (c * (floorDiv x c)).val = c * (x.val / c) := by
  rcases p with _ | n; cases p_ne_zero rfl
  have : c * (x.val / c) ≤ x.val := Nat.mul_div_le (ZMod.val x) ↑c
  have h : x.val < n + 1 := x.is_lt
  rw [ZMod.val_mul, ZMod.val_cast_of_lt lt,
    floorDiv_val, Nat.mod_eq_of_lt (by linarith)]

theorem mod_add_floorDiv {x : F p} {c : ℕ+} (lt : c < p) :
    mod x c lt + c * (floorDiv x c) = x := by
  rcases p with _ | n; cases p_ne_zero rfl
  have h : x.val < n + 1 := x.is_lt
  apply ext
  suffices x.val % c + (c * (floorDiv x c)).val = x.val by
    rw [← mod_val (lt := lt)] at this
    rwa [ZMod.val_add_of_lt]
    rwa [this]
  rw [val_mul_floorDiv_self lt, Nat.mod_add_div x.val c]

theorem mul_val_of_dvd {x c : F p} :
    c.val ∣ (c * x).val → (c * x).val = c.val * x.val := by
  by_cases c_pos? : c = 0
  · subst c_pos?; simp
  intro ⟨x', h_eq⟩
  have h_lt : c.val * x' < p := by
    rw [←h_eq]
    apply ZMod.val_lt
  have c_pos : c.val > 0 := ZMod.val_pos.mpr c_pos?
  have x'_lt : x' < p := calc x'
    _ = 1 * x' := by ring
    _ ≤ c.val * x' := by apply Nat.mul_le_mul_right x' (Nat.succ_le_of_lt c_pos)
    _ < p := h_lt
  let x'_f : F p := natToField x' x'_lt
  have x'_val_eq : x'_f.val = x' := natToField_eq x'_f rfl
  have cx_val_eq : (c * x'_f).val = c.val * x' := by
    rw [←x'_val_eq]
    apply ZMod.val_mul_of_lt
    rw [x'_val_eq]
    exact h_lt
  rw [←cx_val_eq] at h_eq
  obtain h_eq := ext h_eq
  rw [mul_right_inj' c_pos?] at h_eq
  rw [h_eq, x'_val_eq, cx_val_eq]

theorem mul_nat_val_of_dvd {x : F p} (c : ℕ) (c_lt : c < p) {z : ℕ} :
    (c * x).val = c * z → (c * x).val = c * x.val := by
  have c_val_eq : c = (c : F p).val := by rw [ZMod.val_cast_of_lt c_lt]
  rw (occs := .pos [2, 4]) [c_val_eq]
  intro h_dvd
  exact mul_val_of_dvd ⟨ z, h_dvd ⟩

theorem mul_nat_val_eq_iff {x : F p} (c : ℕ) (c_pos : c > 0) (c_lt : c < p) {z : ℕ} :
    (c * x).val = c * z ↔ (x.val = z ∧ c * x.val < p) := by
  constructor
  · intro h_dvd
    constructor
    · apply (mul_right_inj' (Nat.ne_zero_iff_zero_lt.mpr c_pos)).mp
      rw [←h_dvd, mul_nat_val_of_dvd c c_lt h_dvd]
    · rw [←mul_nat_val_of_dvd c c_lt h_dvd]
      apply ZMod.val_lt
  · intro ⟨ h_eq, h_lt ⟩
    have c_val_eq : c = (c : F p).val := by rw [ZMod.val_cast_of_lt c_lt]
    rw [←h_eq, ZMod.val_mul_of_lt, ←c_val_eq]
    rw [←c_val_eq]
    exact h_lt

-- some helper lemmas about 2, using the common assumption that p > 512

section
variable [Fact (p > 512)]

omit p_prime in
lemma two_lt : 2 < p := by
  linarith [‹Fact (p > 512)›.elim]

instance : Fact (p > 2) := .mk two_lt

lemma two_val : (2 : F p).val = 2 :=
  FieldUtils.val_lt_p 2 two_lt

omit p_prime in
lemma two_pow_lt (n : ℕ) (hn : n ≤ 8) : 2^n < p := by
  have h : 2^n ≤ 2^8 := Nat.pow_le_pow_of_le (a:=2) Nat.one_lt_ofNat hn
  linarith [‹Fact (p > 512)›.elim]

lemma two_pow_val (n : ℕ) (hn : n ≤ 8) : (2^n : F p).val = 2^n := by
  rw [ZMod.val_pow, two_val]
  rw [two_val]
  exact two_pow_lt _ hn
end

section
-- lemmas that only assume p > 2
variable [gt2 : Fact (p > 2)]

-- lemmas to resolve (if -1 = 1 then T else E) = E
-- this is commonly needed to resolve the right statement in channel interactions,
-- which split on the multiplicity (which is often 1 or -1)

@[circuit_norm]
lemma neg_one_ne_one :
    ¬(-1 : F p) = 1 := by
  intro h_eq
  suffices h : (1 : F p) + 1 = 0 by
    replace h := congrArg ZMod.val h
    rw [ZMod.val_add] at h
    simp [ZMod.val_one, Nat.mod_eq_of_lt gt2.elim] at h
  nth_rw 1 [← h_eq]
  ring

@[circuit_norm]
lemma one_ne_neg_one : ¬(1 : F p) = -1 :=
  fun h => neg_one_ne_one h.symm
end

end FieldUtils

-- utils related to bytes, and specifically field elements that are bytes (< 256)
namespace ByteUtils
open FieldUtils
variable {p : ℕ} [p_prime: Fact p.Prime]

def mod256 (x : F p) [p_large_enough: Fact (p > 512)] : F p :=
  mod x 256 (by linarith [p_large_enough.elim])

theorem mod256_lt [Fact (p > 512)] (x : F p) : (mod256 x).val < 256 := mod_lt

theorem floorDiv256_bool [Fact (p > 512)] {x : F p} (h : x.val < 512) :
    floorDiv x 256 = 0 ∨ floorDiv x 256 = 1 := by
  rcases p with _ | n; cases p_ne_zero rfl
  let z := x.val / 256
  have : z < 2 := Nat.div_lt_of_lt_mul h
  -- show z = 0 ∨ z = 1
  rcases (Nat.lt_trichotomy z 1) with _ | h1 | _
  · left; apply ext; rw [floorDiv_val]; show z = 0; linarith
  · right; apply ext; rw [floorDiv_val, ZMod.val_one]; exact h1
  · linarith -- contradiction

theorem mod_add_div256 [Fact (p > 512)] (x : F p) : mod256 x + 256 * floorDiv x 256 = x := by
  apply FieldUtils.mod_add_floorDiv (c:=256)

def splitTwoBytes (i : Fin (256 * 256)) : Fin 256 × Fin 256 :=
  let x := i.val / 256
  let y := i.val % 256
  have x_lt : x < 256 := by simp [x, Nat.div_lt_iff_lt_mul]
  have y_lt : y < 256 := Nat.mod_lt i.val (by norm_num)
  (⟨ x, x_lt ⟩, ⟨ y, y_lt ⟩)

def concatTwoBytes (x y : Fin 256) : Fin (256 * 256) :=
  let i := x.val * 256 + y.val
  have i_lt : i < 256 * 256 := by
    unfold i
    linarith [x.is_lt, y.is_lt]
  ⟨ i, i_lt ⟩

theorem splitTwoBytes_concatTwoBytes (x y : Fin 256) : splitTwoBytes (concatTwoBytes x y) = (x, y) := by
  dsimp [splitTwoBytes, concatTwoBytes]
  ext
  simp only
  rw [mul_comm]
  have h := Nat.mul_add_div (by norm_num : 256 > 0) x.val y.val
  rw [h]
  simp
  simp only [Nat.mul_add_mod_of_lt y.is_lt]

theorem byte_sum_do_not_wrap (x y : F p) [Fact (p > 512)]:
    x.val < 256 -> y.val < 256 -> (x + y).val = x.val + y.val := by field_to_nat

theorem byte_sum_le_bound (x y : F p) [Fact (p > 512)]:
    x.val < 256 -> y.val < 256 -> (x + y).val < 511 := by field_to_nat

theorem byte_sum_and_bit_do_not_wrap (x y b : F p) [Fact (p > 512)]:
    x.val < 256 -> y.val < 256 -> b.val < 2 -> (b + x + y).val = b.val + x.val + y.val := by field_to_nat

theorem byte_sum_and_bit_do_not_wrap' (x y b : F p) [Fact (p > 512)]:
    x.val < 256 -> y.val < 256 -> b.val < 2 -> (x + y + b).val = x.val + y.val + b.val := by field_to_nat

theorem byte_sum_and_bit_lt_512 (x y b : F p) [Fact (p > 512)]:
    x.val < 256 -> y.val < 256 -> b.val < 2 -> (x + y + b).val < 512 := by field_to_nat

theorem byte_plus_256_do_not_wrap (x : F p) [Fact (p > 512)]:
    x.val < 256 -> (x + 256).val = x.val + 256 := by field_to_nat

section
variable [p_large_enough: Fact (p > 512)]

def fromByte (x : Fin 256) : F p :=
  FieldUtils.natToField x.val (by linarith [x.is_lt, p_large_enough.elim])

lemma fromByte_lt (x : Fin 256) : (fromByte (p:=p) x).val < 256 := by
  dsimp [fromByte]
  rw [natToField_val]
  exact x.is_lt

lemma fromByte_eq (x : F p) (x_lt : x.val < 256) : fromByte ⟨ x.val, x_lt ⟩ = x := by
  dsimp [fromByte]
  apply FieldUtils.natToField_of_val_eq_iff

end
end ByteUtils
