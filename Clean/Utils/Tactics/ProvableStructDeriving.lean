module

public meta import Lean
public meta import Lean.Elab.Deriving.Util
public meta import Clean.Circuit.Provable

/-!
  # Deriving handlers for ProvableStruct and CircuitType

  This file defines deriving handlers for record-shaped circuit data.

  The `ProvableStruct` macro generates `ProvableStruct` instances for structures where all fields
  are of the form `M F` where `M : TypeMap` (i.e., `M : Type → Type`), and each `M`
  must have a `ProvableType M` instance.

  The `CircuitType` macro generates companion `Var`, `Value`, and `ProverValue`
  structures, plus a `CircuitType` instance, for structures whose fields implement
  `CircuitType`. This allows circuit inputs to mix ordinary provable data with
  prover-only hints such as `UnconstrainedNative`.

  ## Basic usage

  ```lean
  structure MyState (F : Type) where
    pc : F
    ap : F
    fp : F
  deriving ProvableStruct
  ```

  Generates:
  ```lean
  instance : ProvableStruct MyState where
    components := [field, field, field]
    toComponents := fun ⟨pc, ap, fp⟩ => .cons pc (.cons ap (.cons fp .nil))
    fromComponents := fun (.cons pc (.cons ap (.cons fp .nil))) => MyState.mk pc ap fp
  ```

  For `CircuitType`:

  ```lean
  structure Inputs (F : Type) where
    someElement : U32 F
    someHint : UnconstrainedNative Bool F
  deriving CircuitType
  ```

  Generates companion views equivalent to:

  ```lean
  structure Inputs.Var (F : Type) where
    someElement : Var U32 F
    someHint : ProverEnvironment F → Bool

  structure Inputs.Value (F : Type) where
    someElement : Value U32 F
    someHint : Unit

  structure Inputs.ProverValue (F : Type) where
    someElement : ProverValue U32 F
    someHint : Bool
  ```

  ## Extra type parameters

  Supports structures with additional parameters before `F`:

  ```lean
  structure Inputs (n : ℕ) (F : Type) where
    data : Vector F n
  deriving ProvableStruct
  -- Generates: instance {n : ℕ} : ProvableStruct (Inputs n)
  ```

  For `TypeMap` parameters, automatically adds `ProvableType` constraints:

  ```lean
  structure Inputs (M : TypeMap) (F : Type) where
    value : M F
  deriving ProvableStruct
  -- Generates: instance {M : TypeMap} [ProvableType M] : ProvableStruct (Inputs M)
  ```

  ## Vector field types

  - `Vector F n` → mapped to `fields n` (eval expands to element-wise map)
  - `Vector (M F) n` → mapped to `ProvableVector M n` (eval stays unexpanded)

  ```lean
  structure State (F : Type) where
    data : Vector F 8           -- becomes: fields 8
    words : Vector (U32 F) 16   -- becomes: ProvableVector U32 16
  deriving ProvableStruct
  ```

  ## Controlling eval expansion with type aliases

  For `Vector F n` fields, the derived instance uses `fields n`, which causes `eval`
  to expand to `Vector.map (Expression.eval env)` under `circuit_norm`. If you need
  `eval` to stay unexpanded (e.g., for certain proof patterns), define a type alias:

  ```lean
  @[reducible] def MyBuffer := ProvableVector field 64

  structure Inputs (F : Type) where
    buffer : MyBuffer F   -- eval stays unexpanded
  deriving ProvableStruct
  ```

  This pattern is used in the codebase for types like `BLAKE3State`, `KeccakState`, etc.
-/

public meta section

open Lean Meta Elab Term Command Parser.Term

namespace ProvableStructDeriving

/-- Use a declaration name relative to the current namespace when generating commands. -/
def relativeToCurrentNamespace (name : Name) : CommandElabM Name := do
  let ns ← getCurrNamespace
  if ns != .anonymous && ns.isPrefixOf name then
    return name.replacePrefix ns .anonymous
  return name

/--
  Information about a structure parameter (other than the final F : Type parameter)
-/
inductive ParamInfo where
  | natural : Name → ParamInfo      -- (n : ℕ)
  | typeMap : Name → ParamInfo      -- (M : TypeMap) - needs [ProvableType M]
  | other : Name → Expr → ParamInfo -- other type parameter
deriving Inhabited

def ParamInfo.name : ParamInfo → Name
  | .natural n => n
  | .typeMap n => n
  | .other n _ => n

def mkAppliedInductiveWithoutFieldParam (indInfo : InductiveVal) (paramInfos : Array ParamInfo) :
    CommandElabM (TSyntax `term) := do
  if paramInfos.isEmpty then
    return mkIdent indInfo.name
  liftTermElabM do
    Lean.Elab.Deriving.mkInductiveApp indInfo (paramInfos.map ParamInfo.name)

/--
  Analyze a field type to determine its TypeMap.
  `numParams` is the total number of parameters (including F)
  `fieldType` is the type of the field (may contain bvars referring to params)

  The parameters are indexed as: param 0, param 1, ..., param (numParams-2), F (at numParams-1)
  In bvar representation (when abstracted), F is bvar 0, param (numParams-2) is bvar 1, etc.

  Actually, in forallTelescope the args are fvars, so we compare fvars directly by position.
-/
def analyzeFieldType (numParams : Nat) (paramFVars : Array Expr) (fieldType : Expr) : MetaM (TSyntax `term) := do
  -- The type param F is at index (numParams - 1) in paramFVars
  let typeParamIdx := numParams - 1
  let typeParamFVar := paramFVars[typeParamIdx]!
  let otherParamFVars := paramFVars[:typeParamIdx]

  -- Check if the field type is exactly the type parameter F
  if fieldType == typeParamFVar then
    `(field)
  else
    -- Check if it's an application
    match fieldType with
    | .app f arg =>
      -- Check if it's Vector applied to something
      match f with
      | .app (.const ``Vector _) innerType =>
        -- This is `Vector innerType arg` where arg should be the size
        -- innerType could be F or (M F)
        let sizeSyntax ← exprToSyntax otherParamFVars arg

        if innerType == typeParamFVar then
          -- Vector F n -> fields n
          `(fields $sizeSyntax)
        else
          -- Check if innerType is (M F)
          match innerType with
          | .app m innerArg =>
            if innerArg == typeParamFVar then
              -- Vector (M F) n -> ProvableVector M n
              let mSyntax ← exprToSyntax otherParamFVars m
              `(ProvableVector $mSyntax $sizeSyntax)
            else
              throwError "unsupported Vector inner type: {innerType}. Expected F or M F."
          | _ =>
            throwError "unsupported Vector inner type: {innerType}. Expected F or M F."

      | _ =>
        -- Check if the argument is the type parameter (M F pattern)
        if arg == typeParamFVar then
          let fSyntax ← exprToSyntax otherParamFVars f
          return fSyntax
        else
          throwError "field type argument is not the type parameter: {fieldType}"
    | _ =>
      throwError "unsupported field type for ProvableStruct or CircuitType: {fieldType}. Expected either F, M F, Vector F n, or Vector (M F) n where F is the type parameter."
where
  /-- Convert an expression to syntax, handling fvars that reference other parameters -/
  exprToSyntax (paramFVars : Array Expr) (e : Expr) : MetaM (TSyntax `term) := do
    -- First, try to extract a natural number literal (handles elaborated OfNat.ofNat)
    if let some n := extractNatLit? e then
      let nLit := Syntax.mkNumLit (toString n)
      return ← `($nLit)

    match e with
    | .const name _ => return mkIdent name
    | .fvar fvarId =>
      -- Check if this fvar is one of our parameters
      for h : i in [:paramFVars.size] do
        if paramFVars[i] == e then
          let name ← fvarId.getUserName
          return mkIdent name
      throwError "unknown free variable in type: {e}"
    | .app f arg =>
      let fSyntax ← exprToSyntax paramFVars f
      let argSyntax ← exprToSyntax paramFVars arg
      `($fSyntax $argSyntax)
    | .lit (.natVal n) =>
      let nLit := Syntax.mkNumLit (toString n)
      `($nLit)
    | _ =>
      throwError "unsupported expression in field type: {e}"

  /-- Try to extract a natural number literal from an expression.
      Handles both raw literals and elaborated OfNat.ofNat expressions. -/
  extractNatLit? (e : Expr) : Option Nat :=
    match e with
    | .lit (.natVal n) => some n
    | .app (.app (.app (.const ``OfNat.ofNat _) _) (.lit (.natVal n))) _ => some n
    | _ => none

/--
  Analyze a parameter to determine its kind (natural number, TypeMap, or other)
-/
def analyzeParam (paramName : Name) (paramType : Expr) : MetaM ParamInfo := do
  -- Check if it's ℕ
  if paramType.isConstOf ``Nat then
    return .natural paramName
  -- Check if it's TypeMap
  if paramType.isConstOf ``TypeMap then
    return .typeMap paramName
  -- Otherwise, it's some other type
  return .other paramName paramType

/--
  Generate the ProvableStruct instance declaration using syntax quotations.
-/
def mkProvableStructInstance (structName : Name) : CommandElabM Unit := do
  let env ← getEnv

  -- Check that it's a structure
  unless isStructure env structName do
    throwError "{structName} is not a structure"

  -- Get structure info
  let some structInfo := getStructureInfo? env structName
    | throwError "failed to get structure info for {structName}"

  -- Get inductive info to check parameters
  let some (.inductInfo indInfo) := env.find? structName
    | throwError "{structName} not found in environment"

  let numParams := indInfo.numParams

  -- Check that the structure has at least one type parameter (F : Type)
  if numParams < 1 then
    throwError "ProvableStruct deriving requires at least one type parameter (F : Type), but {structName} has {numParams}"

  -- Get field names
  let fieldNames := structInfo.fieldNames

  if fieldNames.isEmpty then
    throwError "ProvableStruct deriving requires at least one field"

  let extractNatLit? (e : Expr) : Option Nat :=
    match e with
    | .lit (.natVal n) => some n
    | .app (.app (.app (.const ``OfNat.ofNat _) _) (.lit (.natVal n))) _ => some n
    | _ => none

  let rec fieldTypeToSyntax (args : Array Expr) (e : Expr) : TermElabM (TSyntax `term) := do
    if let some n := extractNatLit? e then
      return Syntax.mkNumLit (toString n)
    if e == args[indInfo.numParams - 1]! then
      return mkIdent `F
    match e with
    | .fvar fvarId =>
      for h : i in [:args.size] do
        if args[i] == e then
          return mkIdent (← fvarId.getUserName)
      throwError "unknown free variable in field type: {e}"
    | .const name _ =>
      return mkIdent name
    | .app f arg =>
      let fSyntax ← fieldTypeToSyntax args f
      let argSyntax ← fieldTypeToSyntax args arg
      `($fSyntax $argSyntax)
    | .lit (.natVal n) =>
      return Syntax.mkNumLit (toString n)
    | _ =>
      PrettyPrinter.delab e

  -- Do all analysis within a single forallTelescope to keep fvars consistent
  let (paramInfos, componentSyntaxes, fieldTypeSyntaxes) ← liftTermElabM do
    forallTelescope indInfo.type fun args _ => do
      -- args are the parameters: param_0, param_1, ..., param_(n-2), F
      -- We need info for all but the last one (F : Type)
      let mut infos : Array ParamInfo := #[]
      for i in [:numParams - 1] do
        let paramFVar := args[i]!
        let paramName ← paramFVar.fvarId!.getUserName
        let paramType ← inferType paramFVar
        let info ← analyzeParam paramName paramType
        infos := infos.push info

      -- Analyze each field to determine its TypeMap
      let mut components : Array (TSyntax `term) := #[]
      let mut fieldTypes : Array (TSyntax `term) := #[]
      for fname in fieldNames do
        -- Get projection function info
        let projFnName := structName ++ fname
        let some (.defnInfo projInfo) := env.find? projFnName
          | throwError "projection {projFnName} not found"

        -- We need to extract the field type from the projection.
        -- The projection has type: ∀ (params...) (self : StructName params), FieldType
        -- We need to instantiate it with our fvars to get the field type
        let fieldType ← forallTelescope projInfo.type fun projArgs projBody => do
          -- projArgs = [param_0, ..., param_(n-1), self]
          -- projBody is the return type (field type)
          -- We need to substitute our args for the param fvars in projBody
          if projArgs.size != numParams + 1 then
            throwError "projection {projFnName} has unexpected arity: {projArgs.size} vs expected {numParams + 1}"

          -- Create substitution: projArgs[i] -> args[i] for i < numParams
          let mut result := projBody
          for i in [:numParams] do
            result := result.replaceFVar projArgs[i]! args[i]!
          return result

        let componentSyntax ← analyzeFieldType numParams args fieldType
        components := components.push componentSyntax
        let fieldTypeSyntax ← fieldTypeToSyntax args fieldType
        fieldTypes := fieldTypes.push fieldTypeSyntax

      return (infos, components, fieldTypes)

  let fieldNameIdents : Array (TSyntax `ident) := fieldNames.map mkIdent

  -- Build the components list syntax: [M1, M2, M3, ...]
  let componentsListSyntax ← `([$[$componentSyntaxes],*])

  -- Build toComponents body: .cons f1 (.cons f2 (.cons f3 .nil))
  let mut toCompBody : TSyntax `term ← `(.nil)
  for i in [:fieldNameIdents.size] do
    let idx := fieldNameIdents.size - 1 - i
    let fname := fieldNameIdents[idx]!
    toCompBody ← `(.cons $fname $toCompBody)

  -- Build fromComponents pattern: (.cons f1 (.cons f2 (.cons f3 .nil)))
  let mut fromCompPat : TSyntax `term ← `(.nil)
  for i in [:fieldNameIdents.size] do
    let idx := fieldNameIdents.size - 1 - i
    let fname := fieldNameIdents[idx]!
    fromCompPat ← `(.cons $fname $fromCompPat)

  -- For fromComponents, we build the struct constructor explicitly: StructName.mk f1 f2 f3
  let structMk := mkIdent (structName ++ `mk)
  let mkAppSyntax ← fieldNameIdents.foldlM (init := (structMk : TSyntax `term)) fun acc fname =>
    `($acc $fname)

  -- For toComponents, we use anonymous constructor pattern ⟨f1, f2, f3⟩
  -- Build it manually using proper syntax
  let structPatFields := fieldNameIdents.map (TSyntax.mk ·.raw)
  let structPat ← `(⟨$[$structPatFields],*⟩)

  -- Build the applied structure type if there are extra parameters
  -- e.g., Inputs n M for structure Inputs (n : ℕ) (M : TypeMap) (F : Type)
  let appliedStructType ← mkAppliedInductiveWithoutFieldParam indInfo paramInfos

  -- Build the instance binders for extra parameters using bracketedBinderF
  -- e.g., {n : ℕ} {M : TypeMap} [ProvableType M]
  let mut binderSyntaxes : Array (TSyntax ``bracketedBinder) := #[]
  for info in paramInfos do
    match info with
    | .natural n =>
      let nIdent := mkIdent n
      let binder ← `(bracketedBinderF| {$nIdent : ℕ})
      binderSyntaxes := binderSyntaxes.push binder
    | .typeMap m =>
      let mIdent := mkIdent m
      let typeBinder ← `(bracketedBinderF| {$mIdent : TypeMap})
      let instBinder ← `(bracketedBinderF| [ProvableType $mIdent])
      binderSyntaxes := binderSyntaxes.push typeBinder
      binderSyntaxes := binderSyntaxes.push instBinder
    | .other n ty =>
      let nIdent := mkIdent n
      -- For other types, we need to convert the type expression to syntax
      let tySyntax ← liftTermElabM <| PrettyPrinter.delab ty
      let binder ← `(bracketedBinderF| {$nIdent : $tySyntax})
      binderSyntaxes := binderSyntaxes.push binder

  -- Generate a public simp lemma in terms of the original field types, avoiding
  -- the private `fromComponents.match_1` equation theorem whose implicit
  -- arguments expose less-normal component types.
  let theoremFIdent : TSyntax `ident := mkIdent `F
  let theoremFTerm : TSyntax `term := TSyntax.mk theoremFIdent.raw
  let theoremFBinder ← `(bracketedBinderF| {$theoremFIdent:ident : Type})

  let mut fieldBinderSyntaxes : Array (TSyntax ``bracketedBinder) := #[]
  for i in [:fieldNameIdents.size] do
    let fname := fieldNameIdents[i]!
    let fieldType := fieldTypeSyntaxes[i]!
    let binder ← `(bracketedBinderF| ($fname : $fieldType))
    fieldBinderSyntaxes := fieldBinderSyntaxes.push binder

  let mut theoremFromCompTerm : TSyntax `term ← `(.nil)
  for i in [:fieldNameIdents.size] do
    let idx := fieldNameIdents.size - 1 - i
    let fname := fieldNameIdents[idx]!
    theoremFromCompTerm ← `(.cons $fname $theoremFromCompTerm)

  let theoremMkAppSyntax ← fieldNameIdents.foldlM (init := (structMk : TSyntax `term)) fun acc fname =>
    `($acc $fname)

  let theoremName ← relativeToCurrentNamespace (structName ++ `fromComponents_cons)
  let theoremIdent := mkIdent theoremName

  -- Build the full instance command
  let cmd ←
    if binderSyntaxes.isEmpty then
      `(
        instance : ProvableStruct $appliedStructType where
          components := $componentsListSyntax
          toComponents := fun $structPat => $toCompBody
          fromComponents := fun ($fromCompPat) => $mkAppSyntax
      )
    else
      `(
        instance $binderSyntaxes:bracketedBinder* : ProvableStruct $appliedStructType where
          components := $componentsListSyntax
          toComponents := fun $structPat => $toCompBody
          fromComponents := fun ($fromCompPat) => $mkAppSyntax
      )

  elabCommand cmd

  let theoremCmd ← `(
      @[circuit_norm]
      theorem $theoremIdent:ident $binderSyntaxes:bracketedBinder* $theoremFBinder:bracketedBinder
          $fieldBinderSyntaxes:bracketedBinder* :
          fromComponents (α := $appliedStructType) (F := $theoremFTerm)
            (($theoremFromCompTerm : ProvableStruct.ProvableTypeList $theoremFTerm $componentsListSyntax)) =
            $theoremMkAppSyntax := by
        rfl
    )

  elabCommand theoremCmd

/-- The deriving handler for ProvableStruct -/
def provableStructDerivingHandler (declNames : Array Name) : CommandElabM Bool := do
  if declNames.size != 1 then
    return false
  let declName := declNames[0]!
  let env ← getEnv
  -- Check if it's a structure
  unless isStructure env declName do
    return false
  try
    mkProvableStructInstance declName
    return true
  catch e =>
    -- Log error and return false
    logError m!"Failed to derive ProvableStruct for {declName}: {e.toMessageData}"
    return false

-- Register the deriving handler
initialize registerDerivingHandler ``ProvableStruct provableStructDerivingHandler

/--
  Generate a companion structure for one of the `CircuitType` views of a
  derived `CircuitType`.
-/
def mkCircuitViewStruct (viewName : Name) (paramInfos : Array ParamInfo)
    (fieldNameIdents : Array (TSyntax `ident)) (fieldTypes : Array (TSyntax `term))
    (viewType : TSyntax `term → CommandElabM (TSyntax `term)) : CommandElabM Unit := do
  let mut binderSyntaxes : Array (TSyntax ``bracketedBinder) := #[]
  for info in paramInfos do
    match info with
    | .natural n =>
      let nIdent := mkIdent n
      let binder ← `(bracketedBinderF| ($nIdent : ℕ))
      binderSyntaxes := binderSyntaxes.push binder
    | .typeMap m =>
      let mIdent := mkIdent m
      let typeBinder ← `(bracketedBinderF| ($mIdent : TypeMap))
      let instBinder ← `(bracketedBinderF| [CircuitType $mIdent])
      binderSyntaxes := binderSyntaxes.push typeBinder
      binderSyntaxes := binderSyntaxes.push instBinder
    | .other n ty =>
      let nIdent := mkIdent n
      let tySyntax ← liftTermElabM <| PrettyPrinter.delab ty
      let binder ← `(bracketedBinderF| ($nIdent : $tySyntax))
      binderSyntaxes := binderSyntaxes.push binder

  let fIdent := mkIdent `F
  let fBinder ← `(bracketedBinderF| ($fIdent : Type))
  binderSyntaxes := binderSyntaxes.push fBinder

  let mut fieldSyntaxes : Array (TSyntax ``Lean.Parser.Command.structSimpleBinder) := #[]
  for h : i in [:fieldNameIdents.size] do
    let fname := fieldNameIdents[i]
    let component := fieldTypes[i]!
    let ty ← viewType component
    let field ← `(Lean.Parser.Command.structSimpleBinder| $fname:ident : $ty)
    fieldSyntaxes := fieldSyntaxes.push field

  let viewIdent := mkIdent (← relativeToCurrentNamespace viewName)
  let cmd ← `(
    structure $viewIdent $binderSyntaxes:bracketedBinder* where
      $fieldSyntaxes:structSimpleBinder*
  )
  elabCommand cmd

/--
  Generate a `ProvableStruct` instance for the verifier `Value` companion of a
  derived `CircuitType`.

  We generate this from the original field components instead of asking the
  ordinary `ProvableStruct` deriver to rediscover them from fields like
  `CircuitType.Value M F`. The latter loses the semantic context that these are
  verifier values and can get stuck on generated typeclass arguments.
-/
def mkCircuitValueProvableStructInstance (valueStructName : Name) (paramInfos : Array ParamInfo)
    (fieldNameIdents : Array (TSyntax `ident)) (componentSyntaxes : Array (TSyntax `term)) :
    CommandElabM Unit := do
  let valueComponents ← componentSyntaxes.mapM fun component =>
    `(CircuitType.Value $component)
  let componentsListSyntax ← `([$[$valueComponents],*])

  let mut toCompBody : TSyntax `term ← `(.nil)
  for i in [:fieldNameIdents.size] do
    let idx := fieldNameIdents.size - 1 - i
    let fname := fieldNameIdents[idx]!
    toCompBody ← `(.cons $fname $toCompBody)

  let mut fromCompPat : TSyntax `term ← `(.nil)
  for i in [:fieldNameIdents.size] do
    let idx := fieldNameIdents.size - 1 - i
    let fname := fieldNameIdents[idx]!
    fromCompPat ← `(.cons $fname $fromCompPat)

  let valueStructIdent := mkIdent (← relativeToCurrentNamespace valueStructName)
  let valueStructMk := mkIdent (← relativeToCurrentNamespace (valueStructName ++ `mk))
  let mkAppSyntax ← fieldNameIdents.foldlM (init := (valueStructMk : TSyntax `term)) fun acc fname =>
    `($acc $fname)

  let structPatFields := fieldNameIdents.map (TSyntax.mk ·.raw)
  let structPat ← `(⟨$[$structPatFields],*⟩)

  let appliedValueStructType : TSyntax `term ←
    if paramInfos.isEmpty then
      `($valueStructIdent)
    else
      let paramIdents := paramInfos.map (fun p => mkIdent p.name)
      paramIdents.foldlM (init := (valueStructIdent : TSyntax `term)) fun acc paramIdent =>
        `($acc $paramIdent)

  let mut binderSyntaxes : Array (TSyntax ``bracketedBinder) := #[]
  for info in paramInfos do
    match info with
    | .natural n =>
      let nIdent := mkIdent n
      let binder ← `(bracketedBinderF| {$nIdent : ℕ})
      binderSyntaxes := binderSyntaxes.push binder
    | .typeMap m =>
      let mIdent := mkIdent m
      let typeBinder ← `(bracketedBinderF| {$mIdent : TypeMap})
      let circuitBinder ← `(bracketedBinderF| [CircuitType $mIdent])
      let provableValueBinder ← `(bracketedBinderF| [ProvableType (CircuitType.Value $mIdent)])
      binderSyntaxes := binderSyntaxes.push typeBinder
      binderSyntaxes := binderSyntaxes.push circuitBinder
      binderSyntaxes := binderSyntaxes.push provableValueBinder
    | .other n ty =>
      let nIdent := mkIdent n
      let tySyntax ← liftTermElabM <| PrettyPrinter.delab ty
      let binder ← `(bracketedBinderF| {$nIdent : $tySyntax})
      binderSyntaxes := binderSyntaxes.push binder

  let cmd ←
    if binderSyntaxes.isEmpty then
      `(
        instance : ProvableStruct $appliedValueStructType where
          components := $componentsListSyntax
          toComponents := fun $structPat => $toCompBody
          fromComponents := fun ($fromCompPat) => $mkAppSyntax
      )
    else
      `(
        instance $binderSyntaxes:bracketedBinder* : ProvableStruct $appliedValueStructType where
          components := $componentsListSyntax
          toComponents := fun $structPat => $toCompBody
          fromComponents := fun ($fromCompPat) => $mkAppSyntax
      )

  elabCommand cmd

/--
  Generate the CircuitType instance declaration.
-/
def mkCircuitTypeInstance (structName : Name) : CommandElabM Unit := do
  let env ← getEnv

  unless isStructure env structName do
    throwError "{structName} is not a structure"

  let some structInfo := getStructureInfo? env structName
    | throwError "failed to get structure info for {structName}"

  let some (.inductInfo indInfo) := env.find? structName
    | throwError "{structName} not found in environment"

  let numParams := indInfo.numParams

  if numParams < 1 then
    throwError "CircuitType deriving requires at least one type parameter (F : Type), but {structName} has {numParams}"

  let fieldNames := structInfo.fieldNames

  if fieldNames.isEmpty then
    throwError "CircuitType deriving requires at least one field"

  let (paramInfos, componentSyntaxes) ← liftTermElabM do
    forallTelescope indInfo.type fun args _ => do
      let mut infos : Array ParamInfo := #[]
      for i in [:numParams - 1] do
        let paramFVar := args[i]!
        let paramName ← paramFVar.fvarId!.getUserName
        let paramType ← inferType paramFVar
        let info ← analyzeParam paramName paramType
        infos := infos.push info

      let mut components : Array (TSyntax `term) := #[]
      for fname in fieldNames do
        let projFnName := structName ++ fname
        let some (.defnInfo projInfo) := env.find? projFnName
          | throwError "projection {projFnName} not found"

        let fieldType ← forallTelescope projInfo.type fun projArgs projBody => do
          if projArgs.size != numParams + 1 then
            throwError "projection {projFnName} has unexpected arity: {projArgs.size} vs expected {numParams + 1}"

          let mut result := projBody
          for i in [:numParams] do
            result := result.replaceFVar projArgs[i]! args[i]!
          return result

        let componentSyntax ← analyzeFieldType numParams args fieldType
        components := components.push componentSyntax

      return (infos, components)

  let fieldNameIdents : Array (TSyntax `ident) := fieldNames.map mkIdent

  let varStructName := structName ++ `Var
  let valueStructName := structName ++ `Value
  let proverValueStructName := structName ++ `ProverValue

  let fIdent := mkIdent `F
  mkCircuitViewStruct varStructName paramInfos fieldNameIdents componentSyntaxes
    (fun component => `(CircuitType.Var $component $fIdent))
  mkCircuitViewStruct valueStructName paramInfos fieldNameIdents componentSyntaxes
    (fun component => `(CircuitType.Value $component $fIdent))
  mkCircuitViewStruct proverValueStructName paramInfos fieldNameIdents componentSyntaxes
    (fun component => `(CircuitType.ProverValue $component $fIdent))
  mkCircuitValueProvableStructInstance valueStructName paramInfos fieldNameIdents componentSyntaxes

  let appliedStructType ← mkAppliedInductiveWithoutFieldParam indInfo paramInfos

  let viewTypeApp (viewName : Name) : CommandElabM (TSyntax `term) := do
    let viewIdent := mkIdent (← relativeToCurrentNamespace viewName)
    if paramInfos.isEmpty then
      `($viewIdent)
    else
      let paramIdents := paramInfos.map (fun p => mkIdent p.name)
      paramIdents.foldlM (init := (viewIdent : TSyntax `term)) fun acc paramIdent =>
        `($acc $paramIdent)

  let varType ← viewTypeApp varStructName
  let valueType ← viewTypeApp valueStructName
  let proverValueType ← viewTypeApp proverValueStructName

  let mut instanceBinders : Array (TSyntax ``bracketedBinder) := #[]
  for info in paramInfos do
    match info with
    | .natural n =>
      let nIdent := mkIdent n
      let binder ← `(bracketedBinderF| {$nIdent : ℕ})
      instanceBinders := instanceBinders.push binder
    | .typeMap m =>
      let mIdent := mkIdent m
      let typeBinder ← `(bracketedBinderF| {$mIdent : TypeMap})
      let instBinder ← `(bracketedBinderF| [CircuitType $mIdent])
      instanceBinders := instanceBinders.push typeBinder
      instanceBinders := instanceBinders.push instBinder
    | .other n ty =>
      let nIdent := mkIdent n
      let tySyntax ← liftTermElabM <| PrettyPrinter.delab ty
      let binder ← `(bracketedBinderF| {$nIdent : $tySyntax})
      instanceBinders := instanceBinders.push binder

  let inputIdent := mkIdent `input
  let envIdent := mkIdent `env
  let valueMk := mkIdent (← relativeToCurrentNamespace (valueStructName ++ `mk))
  let proverValueMk := mkIdent (← relativeToCurrentNamespace (proverValueStructName ++ `mk))

  let mut verifierBody : TSyntax `term := valueMk
  let mut proverBody : TSyntax `term := proverValueMk
  for fname in fieldNameIdents do
    let verifierField ← `($inputIdent.$fname:ident)
    let verifierEval ← `(Eval.eval $envIdent $verifierField)
    verifierBody ← `($verifierBody $verifierEval)

    let proverField ← `($inputIdent.$fname:ident)
    let proverEval ← `(Eval.eval $envIdent $proverField)
    proverBody ← `($proverBody $proverEval)

  let cmd ←
    if instanceBinders.isEmpty then
      `(
        instance : DerivedCircuitType $appliedStructType where
          Var := $varType
          Value := $valueType
          ProverValue := $proverValueType
          evalVerifier := fun $envIdent $inputIdent => $verifierBody
          evalProver := fun $envIdent $inputIdent => $proverBody
      )
    else
      `(
        instance $instanceBinders:bracketedBinder* : DerivedCircuitType $appliedStructType where
          Var := $varType
          Value := $valueType
          ProverValue := $proverValueType
          evalVerifier := fun $envIdent $inputIdent => $verifierBody
          evalProver := fun $envIdent $inputIdent => $proverBody
      )

  elabCommand cmd

/-- The deriving handler for record-shaped `CircuitType`s. -/
def circuitTypeDerivingHandler (declNames : Array Name) : CommandElabM Bool := do
  if declNames.size != 1 then
    return false
  let declName := declNames[0]!
  let env ← getEnv
  unless isStructure env declName do
    return false
  try
    mkCircuitTypeInstance declName
    return true
  catch e =>
    logError m!"Failed to derive CircuitType for {declName}: {e.toMessageData}"
    return false

initialize registerDerivingHandler ``CircuitType circuitTypeDerivingHandler

end ProvableStructDeriving
