module

public import Clean.Types.U32
public import Clean.Circuit.Subcircuit
public import Clean.Gadgets.Rotation32.Theorems
public import Clean.Gadgets.Rotation32.Rotation32Bytes
public import Clean.Gadgets.ByteDecomposition.ByteDecomposition
public import Clean.Circuit.Provable

@[expose] public section

namespace Gadgets.Rotation32Bits
variable {p : ℕ} [Fact p.Prime]
variable [p_large_enough: Fact (p > 2^16 + 2^8)]

instance : Fact (p > 512) := by
  constructor
  linarith [p_large_enough.elim]

open Gadgets.Rotation32.Theorems
open ByteDecomposition (Outputs)
open ByteDecomposition.Theorems (byteDecomposition_lt)

/--
  Rotate the 32-bit integer by `offset` bits
-/
def main (offset : Fin 8) (x : U32 (Expression (F p))) : Circuit (F p) (Var U32 (F p)) := do
  let parts ← Circuit.map x.toLimbs (ByteDecomposition.circuit offset)
  let lows := parts.map Outputs.low
  let highs := parts.map Outputs.high

  let rotated := highs.zip (lows.rotate 1) |>.map fun (high, low) =>
    high + low * ((2^(8-offset.val) : ℕ) : F p)

  return U32.fromLimbs rotated

def Assumptions (input : U32 (F p)) := input.Normalized

def Spec (offset : Fin 8) (x : U32 (F p)) (y : U32 (F p)) :=
  y.value = rotRight32 x.value offset.val
  ∧ y.Normalized

def output (offset : Fin 8) (i0 : ℕ) : U32 (Expression (F p)) :=
  U32.fromLimbs (.ofFn fun ⟨i,_⟩ =>
    (var ⟨i0 + i*2 + 1⟩) + var ⟨i0 + (i + 1) % 4 * 2⟩ * .const ((2^(8-offset.val) : ℕ) : F p))

@[reducible] instance elaborated (off : Fin 8) : ElaboratedCircuit (F p) U32 U32 (main off) := by
  elaborate_circuit_with {
    localLength _ := 8
    output _inputs i0 := output off i0
  } using by
    simp +instances only [circuit_norm]
    intro inputs i0
    apply congrArg U32.fromLimbs
    simp [Vector.ext_iff, Vector.getElem_rotate]

theorem soundness (offset : Fin 8) : Soundness (F p) (main offset) Assumptions (Spec offset) := by
  circuit_proof_start [ByteDecomposition.circuit, ByteDecomposition.elaborated,
    ByteDecomposition.Assumptions, ByteDecomposition.Spec]

  -- targeted rewriting of the assumptions
  rw [U32.ByteVector.normalized_iff] at h_assumptions
  simp only [circuit_norm, U32.ByteVector.getElem_eval_toLimbs, h_input, h_assumptions, true_implies,
    Fin.forall_iff] at h_holds

  set base := ((2^(8-offset.val) : ℕ) : F p)
  have neg_offset_le : 8 - offset.val ≤ 8 := by
    rw [tsub_le_iff_right, le_add_iff_nonneg_right]; apply zero_le

  -- capture the rotation relation in terms of byte vectors
  set x := input
  set y : U32 (F p) := eval env (output (p:=p) offset i₀)
  set xs := x.toLimbs
  set ys := y.toLimbs
  set o := offset.val

  have h_rot_vector (i : ℕ) (hi : i < 4) :
      ys[i].val < 2^8 ∧
      ys[i].val = xs[i].val / 2^o + (xs[(i + 1) % 4].val % 2^o) * 2^(8-o) := by
    simp only [ys, y, output, U32.ByteVector.eval_fromLimbs, U32.ByteVector.toLimbs_fromLimbs,
      Vector.getElem_map, Vector.getElem_ofFn, Expression.eval]
    set high := env.get (i₀ + i * 2 + 1)
    set next_low := env.get (i₀ + (i + 1) % 4 * 2)
    have ⟨⟨_, high_eq⟩, ⟨_, high_lt⟩⟩ := h_holds i hi
    have ⟨⟨next_low_eq, _⟩, ⟨next_low_lt, _⟩⟩ := h_holds ((i + 1) % 4) (Nat.mod_lt _ (by norm_num))
    have next_low_lt' : next_low.val < 2^(8 - (8 - o)) := by rw [Nat.sub_sub_self offset.is_le']; exact next_low_lt
    have ⟨lt, eq⟩ := byteDecomposition_lt (8-o) neg_offset_le high_lt next_low_lt'
    use lt
    rw [eq, high_eq, next_low_eq]

  -- prove that the output is normalized
  have y_norm : y.Normalized := by
    rw [U32.ByteVector.normalized_iff]
    intro i hi
    exact (h_rot_vector i hi).left

  -- finish the proof using our characerization of rotation on byte vectors
  have h_rot_vector' : y.vals = rotRight32_u32 x.vals o := by
    rw [U32.ByteVector.ext_iff, ←rotRight32_bytes_u32_eq]
    intro i hi
    simp only [U32.vals, U32.ByteVector.toLimbs_map, Vector.getElem_map, rotRight32_bytes, Vector.getElem_ofFn]
    exact (h_rot_vector i hi).right

  rw [←U32.vals_valueNat, ←U32.vals_valueNat, h_rot_vector']
  exact ⟨ rotation32_bits_soundness offset.is_lt, y_norm ⟩

theorem completeness (offset : Fin 8) : Completeness (F p) (main offset) Assumptions := by
  circuit_proof_start [ByteDecomposition.circuit, ByteDecomposition.elaborated,
    ByteDecomposition.Assumptions, ByteDecomposition.Spec]

  -- we only have to prove the byte decomposition assumptions
  rw [U32.ByteVector.normalized_iff] at h_assumptions
  simp_all only [U32.ByteVector.getElem_eval_toLimbs, forall_const]

def circuit (offset : Fin 8) : FormalCircuit (F p) U32 U32 where
  main := main offset
  elaborated := elaborated offset
  Assumptions
  Spec := Spec offset
  soundness := soundness offset
  completeness := completeness offset

end Gadgets.Rotation32Bits
