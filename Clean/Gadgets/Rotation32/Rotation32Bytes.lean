module

public import Clean.Types.U32
public import Clean.Gadgets.Rotation32.Theorems
public import Clean.Utils.Primes

@[expose] public section

namespace Gadgets.Rotation32Bytes
variable {p : ℕ} [Fact p.Prime]

/--
  Rotate the 32-bit integer by increments of 8 positions
  This gadget does not introduce constraints
-/
def main (offset : Fin 4) (input : Var U32 (F p)) : Circuit (F p) (Var U32 (F p)) := do
  let ⟨x0, x1, x2, x3⟩ := input

  if offset = 0 then
    return ⟨ x0, x1, x2, x3 ⟩
  else if offset = 1 then
    return ⟨ x1, x2, x3, x0 ⟩
  else if offset = 2 then
    return ⟨ x2, x3, x0, x1 ⟩
  else
    return ⟨ x3, x0, x1, x2 ⟩

def Assumptions (input : U32 (F p)) := input.Normalized

def Spec (offset : Fin 4) (x : U32 (F p)) (y : U32 (F p)) :=
  y.value = rotRight32 x.value (offset.val * 8) ∧ y.Normalized

@[reducible] instance elaborated (off : Fin 4): ElaboratedCircuit (F p) U32 U32 (main off) where
  localLength _ := 0
  output input i0 :=
    let ⟨x0, x1, x2, x3⟩ := input
    match off with
    | 0 => ⟨ x0, x1, x2, x3 ⟩
    | 1 => ⟨ x1, x2, x3, x0 ⟩
    | 2 => ⟨ x2, x3, x0, x1 ⟩
    | 3 => ⟨ x3, x0, x1, x2 ⟩

  subcircuitsConsistent x i0 := by
    obtain ⟨x0, x1, x2, x3⟩ := x
    simp only [main]
    fin_cases off <;> simp only [circuit_norm, Fin.reduceFinMk, Fin.reduceEq]
  channelsLawful := by
    fin_cases off <;> simp only [circuit_norm, main, Fin.reduceFinMk, Fin.reduceEq]

  output_eq := by
    intros
    fin_cases off
    repeat rfl
  localLength_eq := by
    intros
    fin_cases off
    repeat rfl

theorem soundness (off : Fin 4) : Soundness (F p) (main off) Assumptions (Spec off) := by
  rintro i0 env ⟨ x0_var, x1_var, x2_var, x3_var ⟩ ⟨ x0, x1, x2, x3 ⟩ h_inputs as h

  simp only [circuit_norm, explicit_provable_type, U32.mk.injEq] at h_inputs
  obtain ⟨h_x0, h_x1, h_x2, h_x3⟩ := h_inputs
  clear h

  dsimp only [Assumptions, U32.Normalized] at as
  obtain ⟨ h0, h1, h2, h3 ⟩ := as

  simp [circuit_norm, main, Spec, U32.value, -Nat.reducePow]
  constructor
  · fin_cases off <;> (simp_all [explicit_provable_type, rotRight32, circuit_norm, -Nat.reducePow]; omega)
  · fin_cases off <;> simp_all [circuit_norm, explicit_provable_type]

theorem completeness (off : Fin 4) : Completeness (F p) (main off) Assumptions := by
  rintro i0 env ⟨ x0_var, x1_var, x2_var, x3_var ⟩ henv ⟨ x0, x1, x2, x3 ⟩ _
  fin_cases off
  repeat
    intro Assumptions
    simp [main, circuit_norm]

def circuit (off : Fin 4) : FormalCircuit (F p) U32 U32 where
  main := main off
  elaborated := elaborated off
  requirementsChannelsLawful := by
    fin_cases off <;> simp only [circuit_norm, main, Fin.reduceFinMk, Fin.reduceEq]
  Assumptions
  Spec := Spec off
  soundness := soundness off
  completeness := completeness off

end Gadgets.Rotation32Bytes
