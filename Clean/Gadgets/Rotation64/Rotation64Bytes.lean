module

public import Clean.Types.U64
public import Clean.Gadgets.Rotation64.Theorems
public import Clean.Utils.Primes

@[expose] public section

namespace Gadgets.Rotation64Bytes
variable {p : ℕ} [Fact p.Prime]

/--
  Rotate the 64-bit integer by increments of 8 positions
  This gadget does not introduce constraints
-/
def main (offset : Fin 8) (input : Var U64 (F p)) : Circuit (F p) (Var U64 (F p)) := do
  let ⟨x0, x1, x2, x3 , x4, x5, x6, x7⟩ := input

  if offset = 0 then
    return ⟨ x0, x1, x2, x3, x4, x5, x6, x7 ⟩
  else if offset = 1 then
    return ⟨ x1, x2, x3, x4, x5, x6, x7, x0 ⟩
  else if offset = 2 then
    return ⟨ x2, x3, x4, x5, x6, x7, x0, x1 ⟩
  else if offset = 3 then
    return ⟨ x3, x4, x5, x6, x7, x0, x1, x2 ⟩
  else if offset = 4 then
    return ⟨ x4, x5, x6, x7, x0, x1, x2, x3 ⟩
  else if offset = 5 then
    return ⟨ x5, x6, x7, x0, x1, x2, x3, x4 ⟩
  else if offset = 6 then
    return ⟨ x6, x7, x0, x1, x2, x3, x4, x5 ⟩
  else
    return ⟨ x7, x0, x1, x2, x3, x4, x5, x6 ⟩

def Assumptions (input : U64 (F p)) := input.Normalized

def Spec (offset : Fin 8) (x : U64 (F p)) (y : U64 (F p)) :=
  y.value = rotRight64 x.value (offset.val * 8) ∧ y.Normalized

@[reducible] instance elaborated (off : Fin 8): ElaboratedCircuit (F p) U64 U64 (main off) where
  localLength _ := 0
  output input i0 :=
    let ⟨x0, x1, x2, x3, x4, x5, x6, x7⟩ := input
    match off with
    | 0 => ⟨ x0, x1, x2, x3, x4, x5, x6, x7 ⟩
    | 1 => ⟨ x1, x2, x3, x4, x5, x6, x7, x0 ⟩
    | 2 => ⟨ x2, x3, x4, x5, x6, x7, x0, x1 ⟩
    | 3 => ⟨ x3, x4, x5, x6, x7, x0, x1, x2 ⟩
    | 4 => ⟨ x4, x5, x6, x7, x0, x1, x2, x3 ⟩
    | 5 => ⟨ x5, x6, x7, x0, x1, x2, x3, x4 ⟩
    | 6 => ⟨ x6, x7, x0, x1, x2, x3, x4, x5 ⟩
    | 7 => ⟨ x7, x0, x1, x2, x3, x4, x5, x6 ⟩
  subcircuitsConsistent x i0 := by
    obtain ⟨x0, x1, x2, x3, x4, x5, x6, x7⟩ := x
    simp only [main]
    fin_cases off <;> simp only [circuit_norm, Fin.reduceFinMk, Fin.reduceEq]
  channelsLawful := by
    fin_cases off <;> simp only [circuit_norm, main, Fin.reduceFinMk, Fin.reduceEq]

  output_eq := by
    intros
    fin_cases off
    repeat rfl
  localLength_eq := by
    intros
    fin_cases off
    repeat rfl

theorem soundness (off : Fin 8) : Soundness (F p) (main off) Assumptions (Spec off) := by
  rintro i0 env ⟨ x0_var, x1_var, x2_var, x3_var, x4_var, x5_var, x6_var, x7_var ⟩ ⟨ x0, x1, x2, x3, x4, x5, x6, x7 ⟩ h_inputs as h

  simp only [circuit_norm, explicit_provable_type, Vector.map_mk, List.map_toArray, List.map_cons, List.map_nil,
    fromElements, U64.mk.injEq] at h_inputs
  obtain ⟨h_x0, h_x1, h_x2, h_x3, h_x4, h_x5, h_x6, h_x7⟩ := h_inputs
  clear h

  dsimp only [Assumptions, U64.Normalized] at as
  obtain ⟨ h0, h1, h2, h3, h4, h5, h6, h7 ⟩ := as

  simp [circuit_norm, main, Spec, U64.value, -Nat.reducePow]
  constructor
  · fin_cases off <;> (simp_all [explicit_provable_type, rotRight64, U64.Normalized, circuit_norm, -Nat.reducePow]; omega)
  · fin_cases off <;> simp_all [circuit_norm, explicit_provable_type]

theorem completeness (off : Fin 8) : Completeness (F p) (main off) Assumptions := by
  rintro i0 env ⟨ x0_var, x1_var, x2_var, x3_var, x4_var, x5_var, x6_var, x7_var ⟩ henv ⟨ x0, x1, x2, x3, x4, x5, x6, x7 ⟩ _ Assumptions
  fin_cases off <;> simp [main, circuit_norm]

def circuit (off : Fin 8) : FormalCircuit (F p) U64 U64 := {
  main := main off
  elaborated := elaborated off
  requirementsChannelsLawful := by
    fin_cases off <;> simp only [circuit_norm, main, Fin.reduceFinMk, Fin.reduceEq]
  Assumptions
  Spec := Spec off
  soundness := soundness off
  completeness := completeness off
}
end Gadgets.Rotation64Bytes
