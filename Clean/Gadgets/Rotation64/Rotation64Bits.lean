module

public import Clean.Types.U64
public import Clean.Circuit.Subcircuit
public import Clean.Gadgets.Rotation64.Theorems
public import Clean.Circuit.Provable
public import Clean.Gadgets.ByteDecomposition.ByteDecomposition

@[expose] public section

namespace Gadgets.Rotation64Bits
variable {p : ℕ} [Fact p.Prime]
variable [p_large_enough: Fact (p > 2^16 + 2^8)]

instance : Fact (p > 512) := by
  constructor
  linarith [p_large_enough.elim]

open Rotation64.Theorems
open ByteDecomposition (Outputs)
open ByteDecomposition.Theorems (byteDecomposition_lt)
/--
  Rotate the 64-bit integer by `offset` bits
-/
def main (offset : Fin 8) (x : Var U64 (F p)) : Circuit (F p) (Var U64 (F p)) := do
  let parts ← Circuit.map x.toLimbs (ByteDecomposition.circuit offset)
  let lows := parts.map Outputs.low
  let highs := parts.map Outputs.high

  let rotated := highs.zip (lows.rotate 1) |>.map fun (high, low) =>
    high + low * ((2^(8-offset.val) : ℕ) : F p)

  return U64.fromLimbs rotated

def Assumptions (input : U64 (F p)) := input.Normalized

def Spec (offset : Fin 8) (x : U64 (F p)) (y : U64 (F p)) :=
  y.value = rotRight64 x.value offset.val
  ∧ y.Normalized

def output (offset : Fin 8) (i0 : ℕ) : U64 (Expression (F p)) :=
  U64.fromLimbs (.ofFn fun ⟨i,_⟩ =>
    (var ⟨i0 + i*2 + 1⟩) + var ⟨i0 + (i + 1) % 8 * 2⟩ * .const ((2^(8-offset.val) : ℕ) : F p))

@[reducible] instance elaborated (off : Fin 8) : ElaboratedCircuit (F p) U64 U64 (main off) := by
  elaborate_circuit_with {
    localLength _ := 16
    output _inputs i0 := output off i0
  } using by
    simp +instances only [circuit_norm]
    intro inputs i0
    apply congrArg U64.fromLimbs
    simp [Vector.ext_iff, Vector.getElem_rotate]

theorem soundness (offset : Fin 8) : Soundness (F p) (main offset) Assumptions (Spec offset) := by
  circuit_proof_start [ByteDecomposition.circuit, ByteDecomposition.elaborated,
    ByteDecomposition.Assumptions, ByteDecomposition.Spec]

  -- targeted rewriting of the assumptions
  rw [U64.ByteVector.normalized_iff] at h_assumptions
  simp only [circuit_norm, U64.ByteVector.getElem_eval_toLimbs, h_input, h_assumptions, true_implies,
    Fin.forall_iff] at h_holds

  set base := ((2^(8-offset.val) : ℕ) : F p)
  have neg_offset_le : 8 - offset.val ≤ 8 := by
    rw [tsub_le_iff_right, le_add_iff_nonneg_right]; apply zero_le

  -- capture the rotation relation in terms of byte vectors
  set x := input
  set y : U64 (F p) := eval env (output (p:=p) offset i₀)
  set xs := x.toLimbs
  set ys := y.toLimbs
  set o := offset.val

  have h_rot_vector (i : ℕ) (hi : i < 8) :
      ys[i].val < 2^8 ∧
      ys[i].val = xs[i].val / 2^o + (xs[(i + 1) % 8].val % 2^o) * 2^(8-o) := by
    simp only [ys, y, output, U64.ByteVector.eval_fromLimbs, U64.ByteVector.toLimbs_fromLimbs,
      Vector.getElem_map, Vector.getElem_ofFn, Expression.eval]
    set high := env.get (i₀ + i * 2 + 1)
    set next_low := env.get (i₀ + (i + 1) % 8 * 2)
    have ⟨⟨_, high_eq⟩, ⟨_, high_lt⟩⟩ := h_holds i hi
    have ⟨⟨next_low_eq, _⟩, ⟨next_low_lt, _⟩⟩ := h_holds ((i + 1) % 8) (Nat.mod_lt _ (by norm_num))
    have next_low_lt' : next_low.val < 2^(8 - (8 - o)) := by rw [Nat.sub_sub_self offset.is_le']; exact next_low_lt
    have ⟨lt, eq⟩ := byteDecomposition_lt (8 - o) neg_offset_le high_lt next_low_lt'
    use lt
    rw [eq, high_eq, next_low_eq]

  -- prove that the output is normalized
  have y_norm : y.Normalized := by
    rw [U64.ByteVector.normalized_iff]
    intro i hi
    exact (h_rot_vector i hi).left

  -- finish the proof using our characerization of rotation on byte vectors
  have h_rot_vector' : y.vals = rotRight64_u64 x.vals o := by
    rw [U64.ByteVector.ext_iff, ←rotRight64_bytes_u64_eq]
    intro i hi
    simp only [U64.vals, U64.ByteVector.toLimbs_map, Vector.getElem_map, rotRight64_bytes, Vector.getElem_ofFn]
    exact (h_rot_vector i hi).right

  rw [←U64.vals_valueNat, ←U64.vals_valueNat, h_rot_vector']
  exact ⟨ rotation64_bits_soundness offset.is_lt, y_norm ⟩

theorem completeness (offset : Fin 8) : Completeness (F p) (main offset) Assumptions := by
  circuit_proof_start [ByteDecomposition.circuit, ByteDecomposition.elaborated,
    ByteDecomposition.Assumptions, ByteDecomposition.Spec]

  -- we only have to prove the byte decomposition assumptions
  rw [U64.ByteVector.normalized_iff] at h_assumptions
  simp_all only [U64.ByteVector.getElem_eval_toLimbs, forall_const]

def circuit (offset : Fin 8) : FormalCircuit (F p) U64 U64 := {
  main := main offset
  elaborated := elaborated offset
  Assumptions
  Spec := Spec offset
  soundness := soundness offset
  completeness := completeness offset
}

end Gadgets.Rotation64Bits
