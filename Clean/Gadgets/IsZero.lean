module

public import Clean.Circuit.Basic
public import Clean.Circuit.Provable
public import Clean.Circuit.Theorems
public import Clean.Circuit.Loops
public import Clean.Gadgets.IsZeroField
public import Clean.Utils.Field
public import Clean.Utils.Tactics

@[expose] public section

namespace Gadgets.IsZero

variable {F : Type} [FiniteField F] [DecidableEq F]
variable {M : TypeMap} [ProvableType M]

/--
Main circuit that checks if all elements of a ProvableType are zero.
Returns 1 if all elementts are 0, otherwise returns 0.
-/
def main (input : Var M F) : Circuit F (Var field F) := do
  let elemVars := toElements (M:=M) input
  -- Use foldlRange to multiply all IsZero results together
  -- Start with 1, and for each element, multiply by its IsZero result
  let result ← Circuit.foldlRange (size M) (1 : Expression F) fun acc i => do
    let isZeroElem ← IsZeroField.circuit elemVars[i]
    return acc * isZeroElem
  return result

@[reducible]
instance elaborated : ElaboratedCircuit F M field main := by
  elaborate_circuit_with {
    localLength _ := 2 * size M
    output input i₀ := Fin.foldl (size M)
      (fun acc i => acc * varFromOffset field (i₀ + i * 2 + 1)) 1
  } using by simp +arith +instances [circuit_norm]

def Assumptions (_ : M F) : Prop := True

def Spec [DecidableEq (M F)] (input : M F) (output : F) : Prop :=
  output = if input = 0 then 1 else 0

/--
lemma for soundness. Separate because the statement is optimized for induction.
-/
lemma foldl_isZero_eq_one_iff {n : ℕ} {vars : Vector (Expression F) n} {vals : Vector F n}
    {env : Environment F} {i₀ : ℕ}
    (h_eval : Vector.map (Expression.eval env) vars = vals)
    (h_isZero : ∀ (i : Fin n),
      IsZeroField.circuit.Assumptions (Expression.eval (F:=F) env vars[i]) →
        IsZeroField.circuit.Spec (Expression.eval (F:=F) env vars[i])
          (Expression.eval (F:=F) env
            (varFromOffset field (i₀ + i * 2 + 1)))) :
    Expression.eval env
      (Fin.foldl n
        (fun acc i => acc * (varFromOffset field (i₀ + i * 2 + 1) : Var field F))
        1) =
    if ∀ (i : ℕ) (x : i < n), vals[i] = 0 then 1 else 0 := by
  simp only [IsZeroField.circuit] at h_isZero
  induction n generalizing i₀
  · simp only [Fin.foldl_zero, Expression.eval]
    simp only [not_lt_zero, IsEmpty.forall_iff, implies_true, ↓reduceIte]
  · rename_i pre h_ih
    simp only [Fin.foldl_succ_last, Expression.eval]
    let vars_pre := vars.take pre |>.cast (by simp : min pre (pre + 1) = pre)
    let vals_pre := vals.take pre |>.cast (by simp : min pre (pre + 1) = pre)
    have h_eval_pre : Vector.map (Expression.eval env) vars_pre = vals_pre := by
      simp only [Vector.take_eq_extract, add_tsub_cancel_right, Vector.extract_eq_pop,
        Nat.add_one_sub_one, Nat.sub_zero, Vector.cast_cast, Vector.cast_rfl,
        vals_pre, vars_pre, ← h_eval]
      exact Vector.map_pop
    specialize h_ih h_eval_pre (i₀:=i₀)
    simp only [vars_pre, vals_pre] at *
    simp only [Vector.getElem_cast, forall_const] at h_ih
    simp only [Fin.val_castSucc, Fin.val_last]
    specialize h_ih (by
      intro i
      specialize h_isZero i.castSucc
      norm_num at h_isZero ⊢
      exact h_isZero)
    simp only [Vector.getElem_take] at h_ih
    rw [h_ih]
    specialize h_isZero (.last pre) trivial
    norm_num at h_isZero ⊢
    rw [h_isZero]
    split_ifs <;> try rfl
    · rename_i h_smaller h_last h_all
      apply False.elim
      apply h_all
      intro i
      by_cases h : i < pre
      · intro _
        aesop
      intro _
      have : i = pre := by omega
      aesop
    · rename_i h_smaller h_last h_all
      apply False.elim
      apply h_last
      aesop
    · next h_ex h_all => exfalso; exact h_ex (fun i hi => h_all i (by omega))

theorem soundness [DecidableEq (M F)] : Soundness F (main (M:=M)) Assumptions Spec := by
  circuit_proof_start [IsZeroField.circuit]
  simp only [explicit_provable_type, ProvableType.fromElements_eq_iff] at h_input
  conv_rhs =>
    arg 1
    rw [Zero.toOfNat0, OfNat.ofNat]
    simp only [Zero.zero]
    rw [ProvableType.fromElements_eq_iff']
    rw [Vector.ext_iff]
    simp only [Vector.getElem_replicate]
  apply foldl_isZero_eq_one_iff
  · assumption
  · intro i _
    exact h_holds i

theorem completeness : Completeness F (main (M:=M)) Assumptions := by
  circuit_proof_start [IsZeroField.circuit]

def circuit [DecidableEq (M F)] : FormalCircuit F M field where
  main
  elaborated
  Assumptions
  Spec
  soundness
  completeness

end Gadgets.IsZero
