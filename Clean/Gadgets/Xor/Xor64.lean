module

public import Clean.Circuit
public import Clean.Types.U64
public import Clean.Gadgets.Xor.ByteXorTable

@[expose] public section

section
variable {p : ℕ} [Fact p.Prime] [p_large_enough: Fact (p > 512)]

namespace Gadgets.Xor64
open Gadgets.Xor

structure Inputs (F : Type) where
  x: U64 F
  y: U64 F
deriving ProvableStruct

def main (input : Var Inputs (F p)) : Circuit (F p) (Var U64 (F p))  := do
  let z ← witness <|
    let z0 := (input.x.x0.val ^^^ input.y.x0.val).toField
    let z1 := (input.x.x1.val ^^^ input.y.x1.val).toField
    let z2 := (input.x.x2.val ^^^ input.y.x2.val).toField
    let z3 := (input.x.x3.val ^^^ input.y.x3.val).toField
    let z4 := (input.x.x4.val ^^^ input.y.x4.val).toField
    let z5 := (input.x.x5.val ^^^ input.y.x5.val).toField
    let z6 := (input.x.x6.val ^^^ input.y.x6.val).toField
    let z7 := (input.x.x7.val ^^^ input.y.x7.val).toField
    U64.mk z0 z1 z2 z3 z4 z5 z6 z7

  lookup ByteXorTable (input.x.x0, input.y.x0, z.x0)
  lookup ByteXorTable (input.x.x1, input.y.x1, z.x1)
  lookup ByteXorTable (input.x.x2, input.y.x2, z.x2)
  lookup ByteXorTable (input.x.x3, input.y.x3, z.x3)
  lookup ByteXorTable (input.x.x4, input.y.x4, z.x4)
  lookup ByteXorTable (input.x.x5, input.y.x5, z.x5)
  lookup ByteXorTable (input.x.x6, input.y.x6, z.x6)
  lookup ByteXorTable (input.x.x7, input.y.x7, z.x7)
  return z

def Assumptions (input : Inputs (F p)) :=
  let ⟨x, y⟩ := input
  x.Normalized ∧ y.Normalized

def Spec (input : Inputs (F p)) (z : U64 (F p)) :=
  let ⟨x, y⟩ := input
  z.value = x.value ^^^ y.value ∧ z.Normalized

@[reducible]
instance elaborated : ElaboratedCircuit (F p) Inputs U64 main := by
  elaborate_circuit

omit [Fact (Nat.Prime p)] p_large_enough in
theorem soundness_to_u64 {x y z : U64 (F p)}
  (x_norm : x.Normalized) (y_norm : y.Normalized)
  (h_eq :
    z.x0.val = x.x0.val ^^^ y.x0.val ∧
    z.x1.val = x.x1.val ^^^ y.x1.val ∧
    z.x2.val = x.x2.val ^^^ y.x2.val ∧
    z.x3.val = x.x3.val ^^^ y.x3.val ∧
    z.x4.val = x.x4.val ^^^ y.x4.val ∧
    z.x5.val = x.x5.val ^^^ y.x5.val ∧
    z.x6.val = x.x6.val ^^^ y.x6.val ∧
    z.x7.val = x.x7.val ^^^ y.x7.val) : Spec { x, y } z := by
  simp only [Spec]
  have ⟨ hx0, hx1, hx2, hx3, hx4, hx5, hx6, hx7 ⟩ := x_norm
  have ⟨ hy0, hy1, hy2, hy3, hy4, hy5, hy6, hy7 ⟩ := y_norm

  have z_norm : z.Normalized := by
    simp only [U64.Normalized, h_eq]
    exact ⟨ Nat.xor_lt_two_pow (n:=8) hx0 hy0, Nat.xor_lt_two_pow (n:=8) hx1 hy1,
      Nat.xor_lt_two_pow (n:=8) hx2 hy2, Nat.xor_lt_two_pow (n:=8) hx3 hy3,
      Nat.xor_lt_two_pow (n:=8) hx4 hy4, Nat.xor_lt_two_pow (n:=8) hx5 hy5,
      Nat.xor_lt_two_pow (n:=8) hx6 hy6, Nat.xor_lt_two_pow (n:=8) hx7 hy7 ⟩

  suffices z.value = x.value ^^^ y.value from ⟨ this, z_norm ⟩
  simp only [U64.value_xor_horner, x_norm, y_norm, z_norm, h_eq, xor_mul_two_pow]
  ac_rfl

theorem soundness : Soundness (F p) main Assumptions Spec := by
  circuit_proof_start [ByteXorTable]
  rcases input_x with ⟨ x0, x1, x2, x3, x4, x5, x6, x7 ⟩
  rcases input_y with ⟨ y0, y1, y2, y3, y4, y5, y6, y7 ⟩
  simp only [circuit_norm, explicit_provable_type, U64.mk.injEq] at h_input
  obtain ⟨ x_norm, y_norm ⟩ := h_assumptions
  simp only [h_input, circuit_norm, explicit_provable_type] at h_holds
  apply soundness_to_u64 x_norm y_norm
  simp only [circuit_norm, explicit_provable_type]
  simp [h_holds]

omit [Fact (Nat.Prime p)] in
lemma xor_val {x y : F p} (hx : x.val < 256) (hy : y.val < 256) :
    (x.val ^^^ y.val) % p = x.val ^^^ y.val := by
  have h_byte : x.val ^^^ y.val < 256 := Nat.xor_lt_two_pow (n:=8) hx hy
  exact Nat.mod_eq_of_lt (by linarith [p_large_enough.elim])

theorem completeness : Completeness (F p) main Assumptions := by
  circuit_proof_start [ByteXorTable]
  rcases input_x with ⟨ x0, x1, x2, x3, x4, x5, x6, x7 ⟩
  rcases input_y with ⟨ y0, y1, y2, y3, y4, y5, y6, y7 ⟩
  simp only [circuit_norm, explicit_provable_type, U64.mk.injEq] at h_input
  simp only [circuit_norm, U64.Normalized] at h_assumptions
  simp only [h_input, circuit_norm] at h_env ⊢
  simp only [circuit_norm, explicit_provable_type, U64.mk.injEq] at h_env ⊢
  simp_all [circuit_norm, xor_val]

def circuit : FormalCircuit (F p) Inputs U64 where
  main
  elaborated
  Assumptions
  Spec
  soundness
  completeness
end Gadgets.Xor64
