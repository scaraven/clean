module

public import Clean.Circuit.Loops
public import Clean.Gadgets.Addition8.Addition8FullCarry
public import Clean.Types.U64
public import Clean.Gadgets.Addition32.Theorems
public import Clean.Gadgets.Xor.Xor64
public import Clean.Gadgets.Keccak.KeccakState
public import Clean.Specs.Keccak256

@[expose] public section

namespace Gadgets.Keccak256.ThetaC
variable {p : ℕ} [Fact p.Prime] [Fact (p > 512)]

def main (state : Var KeccakState (F p)) : Circuit (F p) (Var KeccakRow (F p)) :=
  .mapFinRange 5 fun i => do
    let c ← Xor64.circuit ⟨state[5*i.val], state[5*i.val + 1]⟩
    let c ← Xor64.circuit ⟨c, state[5*i.val + 2]⟩
    let c ← Xor64.circuit ⟨c, state[5*i.val + 3]⟩
    let c ← Xor64.circuit ⟨c, state[5*i.val + 4]⟩
    return c

def Assumptions (state : KeccakState (F p)) := state.Normalized

def Spec (state : KeccakState (F p)) (out : KeccakRow (F p)) :=
  out.Normalized
  ∧ out.value = Specs.Keccak256.thetaC state.value

@[reducible]
instance elaborated : ElaboratedCircuit (F p) KeccakState KeccakRow main := by
  elaborate_circuit

-- rewrite thetaC as a loop
lemma thetaC_loop (state : Vector ℕ 25) :
    Specs.Keccak256.thetaC state = .mapFinRange 5 fun i =>
      state[5*i.val] ^^^ state[5*i.val + 1] ^^^ state[5*i.val + 2] ^^^ state[5*i.val + 3] ^^^ state[5*i.val + 4] := by
  conv_rhs =>
    rw [← Vector.ofFn_getElem (xs := Vector.mapFinRange 5 _)]
    simp only [Vector.getElem_mapFinRange]
  apply Vector.toList_inj.mp
  rw [Vector.toList_ofFn]
  rfl

theorem soundness : Soundness (F p) main Assumptions Spec := by
  circuit_proof_start [Xor64.circuit, Xor64.Assumptions, Xor64.Spec]

  -- rewrite goal
  apply KeccakRow.normalized_value_ext
  simp only [thetaC_loop, circuit_norm, eval_vector, KeccakState.value]

  -- simplify constraints
  simp only [circuit_norm, eval_vector, Vector.ext_iff] at h_input
  simp only [circuit_norm, h_input] at h_holds
  have state_norm : ∀ {i : ℕ} (hi : i < 25), input[i].Normalized :=
    fun hi => h_assumptions ⟨ _, hi ⟩
  simp only [state_norm, and_self, forall_const, and_true] at h_holds

  intro i
  specialize h_holds i
  aesop

theorem completeness : Completeness (F p) main Assumptions := by
  intro i0 env state_var h_env state h_input state_norm
  simp only [circuit_norm, eval_vector, Vector.ext_iff] at h_input
  simp only [h_input, circuit_norm,
    main, Xor64.circuit, Xor64.Assumptions, Xor64.Spec] at h_env ⊢
  have state_norm : ∀ (i : ℕ) (hi : i < 25), state[i].Normalized := fun i hi => state_norm ⟨ i, hi ⟩
  simp_all

def circuit : FormalCircuit (F p) KeccakState KeccakRow where
  main := main
  elaborated := elaborated
  Assumptions := Assumptions
  Spec := Spec
  soundness := soundness
  completeness := completeness

end Gadgets.Keccak256.ThetaC
