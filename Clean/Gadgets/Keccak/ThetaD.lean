module

public import Clean.Gadgets.Xor.Xor64
public import Clean.Gadgets.Keccak.KeccakState
public import Clean.Gadgets.Rotation64.Rotation64
public import Clean.Specs.Keccak256

@[expose] public section

namespace Gadgets.Keccak256.ThetaD
variable {p : ℕ} [Fact p.Prime] [p_large_enough: Fact (p > 2^16 + 2^8)]

instance : Fact (p > 512) := .mk (by linarith [p_large_enough.elim])

def main (row : Var KeccakRow (F p)) : Circuit (F p) (Var KeccakRow (F p)) := do
  let c0 ← Rotation64.circuit (64 - 1) row[1]
  let c0 ← Xor64.circuit ⟨row[4], c0⟩

  let c1 ← Rotation64.circuit (64 - 1) row[2]
  let c1 ← Xor64.circuit ⟨row[0], c1⟩

  let c2 ← Rotation64.circuit (64 - 1) row[3]
  let c2 ← Xor64.circuit ⟨row[1], c2⟩

  let c3 ← Rotation64.circuit (64 - 1) row[4]
  let c3 ← Xor64.circuit ⟨row[2], c3⟩

  let c4 ← Rotation64.circuit (64 - 1) row[0]
  let c4 ← Xor64.circuit ⟨row[3], c4⟩

  return #v[c0, c1, c2, c3, c4]

@[reducible]
instance elaborated : ElaboratedCircuit (F p) KeccakRow KeccakRow main := by
  elaborate_circuit

def Assumptions (state : KeccakRow (F p)) := state.Normalized

def Spec (row : KeccakRow (F p)) (out : KeccakRow (F p)) : Prop :=
  out.Normalized
  ∧ out.value = Specs.Keccak256.thetaD row.value

theorem soundness : Soundness (F p) main Assumptions Spec := by
  intro i0 env row_var row h_input row_norm h_holds
  simp only [circuit_norm, eval_vector] at h_input
  dsimp only [Assumptions] at row_norm
  dsimp only [circuit_norm, Spec, main, Xor64.circuit, Rotation64.circuit, Fin.reduceSub] at h_holds ⊢
  simp only [circuit_norm, Xor64.Assumptions, Xor64.Spec, Rotation64.Assumptions, Rotation64.Spec] at h_holds
  simp only [and_imp, add_assoc, Nat.reduceAdd] at h_holds
  simp only [circuit_norm, KeccakRow.normalized_iff, KeccakRow.value, eval_vector]

  have s (i : ℕ) (hi : i < 5) : eval env (row_var[i]) = row[i] := by
    rw [←h_input, Vector.getElem_map]

  simp only [s] at h_holds
  obtain ⟨ h_rot0, h_xor0, h_rot1, h_xor1, h_rot2, h_xor2, h_rot3, h_xor3, h_rot4, h_xor4 ⟩ := h_holds

  specialize h_rot0 (row_norm 1)
  specialize h_xor0 (row_norm 4) h_rot0.right
  rw [h_rot0.left] at h_xor0

  specialize h_rot1 (row_norm 2)
  specialize h_xor1 (row_norm 0) h_rot1.right
  rw [h_rot1.left] at h_xor1

  specialize h_rot2 (row_norm 3)
  specialize h_xor2 (row_norm 1) h_rot2.right
  rw [h_rot2.left] at h_xor2

  specialize h_rot3 (row_norm 4)
  specialize h_xor3 (row_norm 2) h_rot3.right
  rw [h_rot3.left] at h_xor3

  specialize h_rot4 (row_norm 0)
  specialize h_xor4 (row_norm 3) h_rot4.right
  rw [h_rot4.left] at h_xor4

  simp [Specs.Keccak256.thetaD, h_xor0, h_xor1, h_xor2, h_xor3, h_xor4, rotLeft64]

theorem completeness : Completeness (F p) main Assumptions := by
  circuit_proof_start [Xor64.circuit, Rotation64.circuit]
  simp only [KeccakRow.normalized_iff] at h_assumptions
  simp_all only [circuit_norm, getElem_eval_vector,
    Xor64.Assumptions, Xor64.Spec, Rotation64.Assumptions, Rotation64.Spec,
    add_assoc, seval, true_and]

def circuit : FormalCircuit (F p) KeccakRow KeccakRow := {
  main, Assumptions, Spec, soundness, completeness
}

end Gadgets.Keccak256.ThetaD
