module

public import Clean.Utils.Tactics
public import Clean.Types.U32
public import Clean.Gadgets.Or.Or8

@[expose] public section

section
variable {p : ℕ} [Fact p.Prime] [p_large_enough : Fact (p > 512)]

namespace Gadgets.Or32
open Gadgets.Or

structure Inputs (F : Type) where
  x : U32 F
  y : U32 F
deriving ProvableStruct

def main (input : Var Inputs (F p)) : Circuit (F p) (Var U32 (F p))  := do
  let z0 ← Or8.circuit ⟨input.x.x0, input.y.x0⟩
  let z1 ← Or8.circuit ⟨input.x.x1, input.y.x1⟩
  let z2 ← Or8.circuit ⟨input.x.x2, input.y.x2⟩
  let z3 ← Or8.circuit ⟨input.x.x3, input.y.x3⟩

  return ⟨z0, z1, z2, z3⟩

def Assumptions (input : Inputs (F p)) :=
  let ⟨x, y⟩ := input
  x.Normalized ∧ y.Normalized

def Spec (input : Inputs (F p)) (z : U32 (F p)) :=
  let ⟨x, y⟩ := input
  z.value = x.value ||| y.value ∧ z.Normalized

instance elaborated : ElaboratedCircuit (F p) Inputs U32 main := by
  elaborate_circuit

theorem soundness : Soundness (F p) main Assumptions Spec := by
  circuit_proof_start [Or8.circuit, Or8.Assumptions, Or8.Spec]

  have l_components := U32.or_componentwise h_assumptions.1 h_assumptions.2
  rcases input_x
  rcases input_y
  simp only [circuit_norm, explicit_provable_type, fromElements,
    U32.mk.injEq] at h_input ⊢ l_components
  simp only [U32.Normalized, circuit_norm, h_input] at *
  rcases h_holds with ⟨h_holds1, h_holds⟩
  specialize h_holds1 (by omega)
  rcases h_holds with ⟨h_holds2, h_holds⟩
  specialize h_holds2 (by omega)
  rcases h_holds with ⟨h_holds3, h_holds4⟩
  specialize h_holds3 (by omega)
  specialize h_holds4 (by omega)
  simp only [h_holds1.2, h_holds2.2, h_holds3.2, h_holds4.2] -- use the Normalized conditions
  simp only [h_holds1.1, h_holds2.1, h_holds3.1, h_holds4.1, l_components]
  ring_nf
  simp

theorem completeness : Completeness (F p) main Assumptions := by
  circuit_proof_start
  rcases input_x
  rcases input_y
  simp only [explicit_provable_type, fromElements, circuit_norm, U32.mk.injEq] at h_input ⊢
  simp only [Or8.circuit, Or8.Assumptions, h_input]
  simp only [U32.Normalized] at h_assumptions
  omega

def circuit : FormalCircuit (F p) Inputs U32 :=
  { main, elaborated, Assumptions, Spec, soundness, completeness }

end Gadgets.Or32
end
